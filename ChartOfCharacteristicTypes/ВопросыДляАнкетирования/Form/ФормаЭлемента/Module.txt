&НаКлиенте
Перем ЗначенияПеречисленияТипыОтветовНаВопрос;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ

// Управляет видимостью страниц и элементов формы
&НаКлиенте
Процедура УправлениеВидимостью()
	
	ВозможенКомментарий = НЕ (Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.НесколькоВариантовИз ИЛИ Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.Текст);
	Элементы.ТребуетсяКомментарий.Доступность  =  ВозможенКомментарий;
	Элементы.Комментарий.Доступность		   = ВозможенКомментарий;
	Если НЕ ВозможенКомментарий Тогда
		Объект.ТребуетсяКомментарий = Ложь;
		Объект.ПояснениеКомментария = "";
	КонецЕсли;	
	ДоступностьНеобходимостьПояснениеКомментария();	
	Элементы.ЗаполнятьАвтоматически.Видимость 	= Объект.Предопределенный;

	
	Если Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.Строка Тогда 
		
		Элементы.ЗависимыеПараметры.ТекущаяСтраница = Элементы.СтраницаСтрока;
		
	ИначеЕсли Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.Число	Тогда
		
		Элементы.ЗависимыеПараметры.ТекущаяСтраница = Элементы.СтраницаЧисло;
		
	ИначеЕсли Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.ЗначениеИнформационнойБазы Тогда
		
		Элементы.ЗависимыеПараметры.ТекущаяСтраница = Элементы.Пустая;
		
	ИначеЕсли Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.ВидКонтактнойИнформации Тогда
		
		Элементы.ЗависимыеПараметры.ТекущаяСтраница = Элементы.КонтактнаяИнформация;		
	
	ИначеЕсли Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.ОдинВариантИз 
	      ИЛИ Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.НесколькоВариантовИз Тогда
		
		Элементы.ЗависимыеПараметры.ТекущаяСтраница = Элементы.ВариантыОтветов; 
		
		ДоступностьТаблицыВариантыОтветов();
		
	Иначе
		
		
		Элементы.ЗависимыеПараметры.ТекущаяСтраница = Элементы.Пустая;
		
	КонецЕсли;
	
	
КонецПроцедуры // УправлениеВидимостью() 

&НаКлиенте
Процедура ДоступностьНеобходимостьПояснениеКомментария()
	
	Элементы.ПояснениеКомментария.АвтоОтметкаНезаполненного = Объект.ТребуетсяКомментарий;
	Элементы.ПояснениеКомментария.ТолькоПросмотр 			= НЕ Объект.ТребуетсяКомментарий;
	
	ОтключитьОтметкуНезаполненного();
	
КонецПроцедуры // ДоступностьНеобходимостьПояснениеКомментария()

&НаКлиенте
Процедура ДоступностьТаблицыВариантыОтветов()
	
	Если Объект.Ссылка.Пустая() Тогда
		Элементы.ТаблицаВариантыОтветов.ТолькоПросмотр  = Истина;
		ИнформацияВариантыОтветов						= НСтр("ru = 'Для редактирования вариантов ответов необходимо записать вопрос для анкетирования'");
	Иначе
		Элементы.ТаблицаВариантыОтветов.ТолькоПросмотр 	= Ложь;
		ИнформацияВариантыОтветов						= НСтр("ru = 'Варианты ответов на вопрос:'");
	КонецЕсли; 
	
	 Если ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.ОдинВариантИз Тогда
		 Элементы.ТребуетОткрытогоОтвета.Видимость = Ложь;		 
	 ИначеЕсли ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.НесколькоВариантовИз Тогда
		 Элементы.ТребуетОткрытогоОтвета.Видимость = Истина;	     	 
	 КонецЕсли;
	
КонецПроцедуры // ВидимостьТаблицыВариантыОтветов()

&НаКлиенте
Процедура ОткрытьФормуЭлементаСправочникаВопросыОтветовАнкет(Элемент,РежимДобавления)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Владелец",Объект.Ссылка);
	СтруктураПараметров.Вставить("ТипОтвета",Объект.ТипОтвета);
	
	Если Не РежимДобавления Тогда
		ТекущиеДанные = Элементы.ТаблицаВариантыОтветов.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
		    Возврат;			
		КонецЕсли;
		СтруктураПараметров.Вставить("Ключ",ТекущиеДанные.Ссылка);
	КонецЕсли;
		
	ОткрытьФорму("Справочник.ВариантыОтветовАнкет.Форма.ФормаЭлемента",СтруктураПараметров,Элемент);
	
КонецПроцедуры // ()

// Процедура получает необходмые значения предопределенных значений,
// необходимых на клиенте.
&НаСервере
Процедура ПолучениеПредопределенныхЗначенийНеобходимыхНаКлиенте()
    	
	ПеречислениеТипыОтветовНаВопрос = Анкетирование.ПолучитьСтруктуруЗначенияПеречисления("ТипыОтветовНаВопрос");
	ПредопределеныеЗначенияНеобходимыеНаКлиенте.Вставить(0,ПеречислениеТипыОтветовНаВопрос); 
	                                                                                                                
КонецПроцедуры // ПредопределеныеЗначенияНеобходимыеНаКлиенте()

&НаСервере
Процедура УстановитьТипОтвета()
	
	Для каждого ЗначениеПеречисления Из ПредопределеныеЗначенияНеобходимыеНаКлиенте.Получить(0).Значение Цикл
		
		Если ЗначениеПеречисления.Значение = Перечисления.ТипыОтветовНаВопрос.ЗначениеИнформационнойБазы Тогда 
			
			Для каждого ДоступныйТип Из РеквизитФормыВЗначение("Объект").Метаданные().Тип.Типы() Цикл
				
				Если ДоступныйТип = Тип("Строка") ИЛИ ДоступныйТип = Тип("Булево") ИЛИ ДоступныйТип = Тип("Число") ИЛИ ДоступныйТип = Тип("Дата") ИЛИ ДоступныйТип = Тип("СправочникСсылка.ВариантыОтветовАнкет") Тогда
					Продолжить;		
				КонецЕсли;
				
				МассивТипов = Новый Массив;
				МассивТипов.Добавить(ДоступныйТип);
				Элементы.ТипОтвета.СписокВыбора.Добавить(Новый ОписаниеТипов(МассивТипов));
				
			КонецЦикла; 
			
		Иначе	
			Элементы.ТипОтвета.СписокВыбора.Добавить(ЗначениеПеречисления.Значение); 		
		КонецЕсли;
		
	КонецЦикла;
	
	Если Объект.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.ЗначениеИнформационнойБазы Тогда
		
		ТипОтвета = Объект.ТипЗначения;
		
	Иначе
		
		ТипОтвета = Объект.ТипОтвета;
		
	КонецЕсли;
	
	
КонецПроцедуры // ()

// Устанавливает точность числового ответа в зависимости от выбранной длины
//
&НаКлиенте
Процедура УстановитьТочностьВЗависимостиОтДлиныЧисла()

	Если Объект.Длина > 15 Тогда
	    Объект.Длина = 15;	
	КонецЕсли;
	
	Если Объект.Длина = 0 Тогда
	    Объект.Точность = 0;
	ИначеЕсли Объект.Длина <= Объект.Точность Тогда
		Объект.Точность = Объект.Длина - 1;
	КонецЕсли;
	
	Если Объект.Точность > 3 Тогда
		Объект.Точность = 3;
	КонецЕсли;
	
	Если (Объект.Длина - Объект.Точность) > 12 Тогда
		Объект.Длина = Объект.Точность + 12;	
    КонецЕсли;
КонецПроцедуры // ()

////////////////////////////////////////////////////////////////////////////////              
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ 

&НаКлиенте                                                                         
Процедура ПриОткрытии(Отказ)
	
	ЗначенияПеречисленияТипыОтветовНаВопрос = ПредопределеныеЗначенияНеобходимыеНаКлиенте.Получить(0).Значение;
	УправлениеВидимостью();
	
КонецПроцедуры

&НаСервере                                                                                       
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//установим отбор по владельцу на динамическом списке справочника "Варианты ответов анкет"
	ЭлементОтбораДанных = ВариантыОтветов.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Владелец");
	ЭлементОтбораДанных.Использование 	= Истина;
	ЭлементОтбораДанных.ВидСравнения  	= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение 	= Объект.Ссылка;
	
	ПолучениеПредопределенныхЗначенийНеобходимыхНаКлиенте();
	УстановитьТипОтвета();
	
	Если ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Строка Тогда
	    ДлинаСтроки = Объект.Длина;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.Число Тогда
		
		Если Объект.МинимальноеЗначение > Объект.МаксимальноеЗначение Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Минимально допустимое значение не может быть больше чем максимальное.'"),,
																"Объект.МинимальноеЗначение");
			Отказ = Истина;
		КонецЕсли;
		
	ИначеЕсли Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.Строка Тогда	
		
		Объект.Длина = ДлинаСтроки;
		Если ДлинаСтроки = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнено значение длины строки.'"),,"ДлинаСтроки");	
			Отказ = Истина;
		КонецЕсли;
		
	ИначеЕсли Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.Текст Тогда
		
		Объект.Длина = 1024;
		
	КонецЕсли;
	
КонецПроцедуры

//////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ 

&НаКлиенте
Процедура ТипОтветаПриИзменении(Элемент)
	
	Если ТипЗнч(ТипОтвета) = Тип("ПеречислениеСсылка.ТипыОтветовНаВопрос") Тогда
		
		Объект.ТипОтвета = ТипОтвета;
		   
	ИначеЕсли ТипЗнч(ТипОтвета) = Тип("ОписаниеТипов") Тогда
		
		Объект.ТипОтвета 	= ЗначенияПеречисленияТипыОтветовНаВопрос.ЗначениеИнформационнойБазы;
		Объект.ТипЗначения 	= ТипОтвета;
		
	КонецЕсли;
	
	УправлениеВидимостью(); 
	
	Если Объект.ТипОтвета = ЗначенияПеречисленияТипыОтветовНаВопрос.Число Тогда
	     УстановитьТочностьВЗависимостиОтДлиныЧисла(); 	
	КонецЕсли;
	
КонецПроцедуры
  
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ДоступностьТаблицыВариантыОтветов();
	ВариантыОтветов.Отбор.Элементы[0].ПравоеЗначение = Объект.Ссылка;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВариантыОтветовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	ОткрытьФормуЭлементаСправочникаВопросыОтветовАнкет(Элемент,Истина); 
	
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяКомментарийПриИзменении(Элемент)
	
	ДоступностьНеобходимостьПояснениеКомментария();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.Формулировка) Тогда
	
		Объект.Формулировка = Объект.Наименование;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВариантыОтветовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьФормуЭлементаСправочникаВопросыОтветовАнкет(Элемент,Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВариантыОтветовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуЭлементаСправочникаВопросыОтветовАнкет(Элемент,Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)
	
	УстановитьТочностьВЗависимостиОтДлиныЧисла();
	
КонецПроцедуры

&НаКлиенте
Процедура ТочностьПриИзменении(Элемент)
	
	УстановитьТочностьВЗависимостиОтДлиныЧисла();
	
КонецПроцедуры
                                                              
 ////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПОДСИСТЕМЫ НАСТРОЙКИ ПОРЯДКА ЭЛЕМЕНТОВ ДЛЯ ПОДЧИНЕННОГО СПРАВОЧНИКА
// ВАРИАНТЫ ОТВЕТОВ АНКЕТ

&НаКлиенте
Процедура ПереместитьЭлементВверх(Команда)
	
	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВверхВыполнить(ВариантыОтветов, Элементы.ТаблицаВариантыОтветов);
	
КонецПроцедуры	

&НаКлиенте
Процедура ПереместитьЭлементВниз(Команда)
	
	НастройкаПорядкаЭлементовКлиент.ПереместитьЭлементВнизВыполнить(ВариантыОтветов, Элементы.ТаблицаВариантыОтветов);
	
КонецПроцедуры

