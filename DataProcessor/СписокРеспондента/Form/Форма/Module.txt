
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ 

&НаСервере
Процедура ПолучитьДеревоАнкетРеспондента()
	
	ДеревоАнкетСервер = РеквизитФормыВЗначение("ДеревоАнкет");
	ДеревоАнкетСервер.Строки.Очистить();
	
	Если  ЗначениеЗаполнено(Объект.Респондент) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	НазначениеОпросов.Ссылка,
		               |	НазначениеОпросов.СвободныйОпрос,
		               |	НазначениеОпросов.ДатаОкончания,
		               |	НазначениеОпросов.Наименование
		               |ПОМЕСТИТЬ ДействующиеОпросы
		               |ИЗ
		               |	Документ.НазначениеОпросов КАК НазначениеОпросов
		               |ГДЕ
		               |	НазначениеОпросов.Проведен
		               |	И (НЕ НазначениеОпросов.ПометкаУдаления)
		               |	И (НазначениеОпросов.ДатаНачала = &ПустаяДата
		               |			ИЛИ НАЧАЛОПЕРИОДА(НазначениеОпросов.ДатаНачала, ДЕНЬ) < &ТекущаяДата)
		               |	И НазначениеОпросов.ТипРеспондентов = &ТипРеспондентов
		               |	И (НазначениеОпросов.ДатаОкончания = &ПустаяДата
		               |			ИЛИ КОНЕЦПЕРИОДА(НазначениеОпросов.ДатаОкончания, ДЕНЬ) > &ТекущаяДата)
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ДействующиеОпросы.Ссылка,
		               |	ДействующиеОпросы.ДатаОкончания,
		               |	ДействующиеОпросы.Наименование
		               |ПОМЕСТИТЬ ДействующиеОпросыОтборПоРеспонденту
		               |ИЗ
		               |	ДействующиеОпросы КАК ДействующиеОпросы
		               |ГДЕ
		               |	ДействующиеОпросы.СвободныйОпрос
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	НазначениеОпросовРеспонденты.Ссылка,
		               |	НазначениеОпросов.ДатаОкончания,
		               |	НазначениеОпросов.Наименование
		               |ИЗ
		               |	Документ.НазначениеОпросов.Респонденты КАК НазначениеОпросовРеспонденты
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НазначениеОпросов КАК НазначениеОпросов
		               |		ПО НазначениеОпросовРеспонденты.Ссылка = НазначениеОпросов.Ссылка
		               |ГДЕ
		               |	НазначениеОпросовРеспонденты.Респондент = &Респондент
		               |	И НазначениеОпросовРеспонденты.Ссылка В
		               |			(ВЫБРАТЬ
		               |				ДействующиеОпросы.Ссылка
		               |			ИЗ
		               |				ДействующиеОпросы КАК ДействующиеОпросы
		               |			ГДЕ
		               |				(НЕ ДействующиеОпросы.СвободныйОпрос))
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВЫБОР
		               |		КОГДА АнкетыПоДействующимЗапросам.Ссылка ЕСТЬ NULL 
		               |			ТОГДА ""Опросы""
		               |		ИНАЧЕ ""Анкеты""
		               |	КОНЕЦ КАК Статус,
		               |	ВЫБОР
		               |		КОГДА АнкетыПоДействующимЗапросам.Ссылка ЕСТЬ NULL 
		               |			ТОГДА ДействующиеОпросыОтборПоРеспонденту.Ссылка
		               |		ИНАЧЕ АнкетыПоДействующимЗапросам.Ссылка
		               |	КОНЕЦ КАК АнкетаОпрос,
		               |	ДействующиеОпросыОтборПоРеспонденту.ДатаОкончания КАК ДатаОкончания,
		               |	ДействующиеОпросыОтборПоРеспонденту.Наименование,
		               |	АнкетыПоДействующимЗапросам.Дата КАК ДатаАнкеты,
		               |	ЕСТЬNULL(АнкетыПоДействующимЗапросам.Проведен, ЛОЖЬ) КАК Проведен
		               |ПОМЕСТИТЬ НеотвеченныеОпросыСохраненныеАнкеты
		               |ИЗ
		               |	ДействующиеОпросыОтборПоРеспонденту КАК ДействующиеОпросыОтборПоРеспонденту
		               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		               |			Анкета.Ссылка КАК Ссылка,
		               |			Анкета.ДатаРедактирования КАК Дата,
		               |			Анкета.Проведен КАК Проведен,
		               |			Анкета.Опрос КАК Опрос
		               |		ИЗ
		               |			Документ.Анкета КАК Анкета
		               |		ГДЕ
		               |			Анкета.Опрос В
		               |					(ВЫБРАТЬ
		               |						ДействующиеОпросыОтборПоРеспонденту.Ссылка
		               |					ИЗ
		               |						ДействующиеОпросыОтборПоРеспонденту КАК ДействующиеОпросыОтборПоРеспонденту)
		               |			И Анкета.Респондент = &Респондент
		               |			И (НЕ Анкета.ПометкаУдаления)) КАК АнкетыПоДействующимЗапросам
		               |		ПО ДействующиеОпросыОтборПоРеспонденту.Ссылка = АнкетыПоДействующимЗапросам.Опрос
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	НеотвеченныеОпросыСохраненныеАнкеты.Статус,
		               |	НеотвеченныеОпросыСохраненныеАнкеты.АнкетаОпрос,
		               |	НеотвеченныеОпросыСохраненныеАнкеты.ДатаОкончания,
		               |	НеотвеченныеОпросыСохраненныеАнкеты.Наименование,
		               |	НеотвеченныеОпросыСохраненныеАнкеты.ДатаАнкеты
		               |ИЗ
		               |	НеотвеченныеОпросыСохраненныеАнкеты КАК НеотвеченныеОпросыСохраненныеАнкеты
		               |ГДЕ
		               |	(НЕ НеотвеченныеОпросыСохраненныеАнкеты.Проведен)";
		
		Запрос.УстановитьПараметр("Респондент",Объект.Респондент );
		Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());
		Запрос.УстановитьПараметр("ПустаяДата",Дата(1,1,1));
		Запрос.УстановитьПараметр("ТипРеспондентов",Справочники[Объект.Респондент.Метаданные().Имя].ПустаяСсылка());
		
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			
			ДеревоАнкетРеспондента = Результат.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
			Для каждого СтрокаДерева Из ДеревоАнкетРеспондента.Строки Цикл
				ДобавитьСтрокуДереваАнкет(СтрокаДерева,ДеревоАнкетСервер);			 
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗначениеВДанныеФормы(ДеревоАнкетСервер,ДеревоАнкет);
	
КонецПроцедуры 

// Формирует представление строки для дерева анкет
//
// Параметры
//  СтрокаДерева  - СтрокаДереваЗначений - на основании ее формируется представление 
//                 анкет и опросов в дереве 
&НаСервере
Функция ПолучитьПредставлениеСтрокиДереваАнкеты(СтрокаДерева)
	
	СтрокаВозврата = "";
	
	ЕстьОграниченияПоСроку = ЗначениеЗаполнено(СтрокаДерева.ДатаОкончания);
	Закончился = ?(ЗначениеЗаполнено(СтрокаДерева.ДатаОкончания),?(ТекущаяДата()<=СтрокаДерева.ДатаОкончания,Ложь,Истина),Ложь);
	
	Если ТипЗнч(СтрокаДерева.АнкетаОпрос) = Тип("ДокументСсылка.НазначениеОпросов") Тогда
		СтрокаВозврата = СтрокаВозврата + "Анкета " + "'"+ СтрокаДерева.Наименование + "'";
	ИначеЕсли ТипЗнч(СтрокаДерева.АнкетаОпрос) = Тип("ДокументСсылка.Анкета") Тогда
		СтрокаВозврата = СтрокаВозврата + "Анкета " + "'" + СтрокаДерева.Наименование + "'" + ", последний раз редактировавшаяся " + Формат(СтрокаДерева.ДатаАнкеты,"ДФ=dd.MM.yyyy");
	Иначе	
		Возврат СтрокаВозврата;	
	КонецЕсли;
	
	Если ЕстьОграниченияПоСроку Тогда
			СтрокаВозврата = СтрокаВозврата + ", к заполнению до " + Формат(НачалоДня(КонецДня(СтрокаДерева.ДатаОкончания) + 1),"ДФ=dd.MM.yyyy");		
	КонецЕсли;
		
	СтрокаВозврата = СтрокаВозврата + ".";	
	
	Возврат СтрокаВозврата;	
	
КонецФункции// 

// Устанавливает значение Респондент, согласно значению текущего внешнего пользователя
&НаСервере
Процедура УстановитьРеспондентаСогласноТекущемуВнешнемуПользователю()
	
	Объект.Респондент = ВнешниеПользователи.ПолучитьОбъектАвторизацииВнешнегоПользователя(ВнешниеПользователи.ТекущийВнешнийПользователь());
	ПолучитьДеревоАнкетРеспондента();
	
КонецПроцедуры 

// Вызыватся рекурсивно, заполняет дерево анкет респондента
//
// Параметры
//  СтрокаДерева  - СтрокаДереваЗначений - строка, которая будет довавлена
//	ДеревоАнкетыСервер - ДеревоЗначений  - дерево, в которое будет добавлена строка
&НаСервере
Процедура ДобавитьСтрокуДереваАнкет(СтрокаДерева,Родитель)
	
	НоваяСтрока = Родитель.Строки.Добавить();
	Если НЕ ЗначениеЗаполнено(СтрокаДерева.АнкетаОпрос) Тогда
		
		НоваяСтрока.Представление = СтрокаДерева.Статус;
		НоваяСтрока.Статус 		  = СтрокаДерева.Статус;
		
	Иначе
		
		НоваяСтрока.Статус 		  		= СтрокаДерева.Статус;
		НоваяСтрока.АнкетаОпрос			= СтрокаДерева.АнкетаОпрос;
		НоваяСтрока.Представление 		= ПолучитьПредставлениеСтрокиДереваАнкеты(СтрокаДерева);
		
	КонецЕсли;
	
	НоваяСтрока.КодКартинки = ?(СтрокаДерева.Статус = "Опросы",0,1);
	
	Если СтрокаДерева.Строки.Количество() > 0 Тогда
		Для каждого ПодчиненнаяСтрокаДерева Из СтрокаДерева.Строки Цикл
			ДобавитьСтрокуДереваАнкет(ПодчиненнаяСтрокаДерева,НоваяСтрока);			
		КонецЦикла;	   
	КонецЕсли;                                                                               
	
КонецПроцедуры // ()

// Разворачивает в дереве анкет группировки "Доступные опросы" и "Незаконченные опросы"
&НаКлиенте
Процедура РазвернутьДеревоАнкет()

	ЭлементыВерхнегоУровня = ДеревоАнкет.ПолучитьЭлементы();
	Для каждого Элемент Из ЭлементыВерхнегоУровня Цикл
		Если Элемент.Представление = "Доступные опросы" 
		 ИЛИ Элемент.Представление = "Незаконченные анкеты" Тогда
		
			  Элементы.ДеревоАнкет.Развернуть(Элемент.ПолучитьИдентификатор());
		
		КонецЕсли;
	КонецЦикла;
	

КонецПроцедуры // РазвернутьДеревоАнкет()

//////////////////////////////////////////////////////////
// КОМАНДЫ ФОРМЫ 

&НаКлиенте
Процедура АрхивАнкет(Команда)
	
	ОткрытьФорму("Обработка.СписокРеспондента.Форма.АрхивАнкет",Новый Структура("Респондент",Объект.Респондент),ЭтаФорма);
	
КонецПроцедуры 

&НаКлиенте
Процедура Обновить(Команда)
	
	ПолучитьДеревоАнкетРеспондента();
	
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьРеспондентаСогласноТекущемуВнешнемуПользователю();
  	Если НЕ ПравоДоступа("Редактирование",РеквизитФормыВЗначение("Объект").Метаданные().Реквизиты.Респондент) Тогда
	  Элементы.Респондент.Видимость = Ложь;
  	КонецЕсли;
	
КонецПроцедуры
 
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОкончаниеРедактированияАнкеты" Тогда		
		ПолучитьДеревоАнкетРеспондента();
	ИначеЕсли ИмяСобытия = "ИзменениеТекущегоВнешнегоПользователя" Тогда
		УстановитьРеспондентаСогласноТекущемуВнешнемуПользователю();
	КонецЕсли;
			
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РазвернутьДеревоАнкет();
	 	
КонецПроцедуры

//////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ 

&НаКлиенте
Процедура ДеревоАнкетПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ДеревоАнкет.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.АнкетаОпрос) = Тип("ДокументСсылка.Анкета") Тогда
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Ключ",ТекущиеДанные.АнкетаОпрос);
		СтруктураПараметров.Вставить("ТолькоФормаЗаполнения",Истина);
		Если ТекущиеДанные.Статус = "Отвеченные анкеты" Тогда
			СтруктураПараметров.Вставить("ТолькоПросмотр",Истина);	
		КонецЕсли;
		
		ОткрытьФорму("Документ.Анкета.Форма.ФормаДокумента",СтруктураПараметров,Элемент);
		
	ИначеЕсли ТипЗнч(ТекущиеДанные.АнкетаОпрос) = Тип("ДокументСсылка.НазначениеОпросов") Тогда	
		
		СтруктураПараметров = Новый Структура;
		ЗначенияЗаполнения 	= Новый Структура;
		
		ЗначенияЗаполнения.Вставить("Респондент",Объект.Респондент);
		ЗначенияЗаполнения.Вставить("Опрос",ТекущиеДанные.АнкетаОпрос);
		СтруктураПараметров.Вставить("ЗначенияЗаполнения",ЗначенияЗаполнения);
		СтруктураПараметров.Вставить("ТолькоФормаЗаполнения",Истина);
		
		ОткрытьФорму("Документ.Анкета.Форма.ФормаДокумента",СтруктураПараметров,Элемент);
		
	КонецЕсли;	
	
КонецПроцедуры

 &НаКлиенте
Процедура РеспондентПриИзменении(Элемент)
	
	ПолучитьДеревоАнкетРеспондента();
	РазвернутьДеревоАнкет();
	
КонецПроцедуры





