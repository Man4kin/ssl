&НаКлиенте
Процедура ЗагрузитьВыполнить()
	
	Если Элементы.СписокВариантовОтчета.ТекущаяСтрока <> Неопределено Тогда 
		
		КлючТекущегоВарианта = Элементы.СписокВариантовОтчета.ТекущиеДанные.КлючВариантаОтчета;
		СписокВыбора = Новый СписокЗначений;
		ВыборНастроек = Новый ВыборНастроек(КлючТекущегоВарианта);
		Закрыть(ВыборНастроек);
		
	Иначе
		
		Закрыть();
		
	КонецЕсли;
		
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	КлючОбъекта               = Параметры.КлючОбъекта;
	КлючТекущихНастроек       = Параметры.КлючТекущихНастроек;             
	СтандартныеНастройки      = Параметры.СтандартныеНастройки;
	ТекущийПользователь       = Параметры.ТекущийПользователь;
	ПоказыватьТолькоИзбранные = Параметры.ПоказыватьТолькоИзбранные;
		
	Если НЕ ЗначениеЗаполнено(ТекущийПользователь) тогда
		ТекущийПользователь           = ОбщегоНазначения.ТекущийПользователь();
		Параметры.ТекущийПользователь = ТекущийПользователь;
	КонецЕсли;
	
	ЗаполнитьСписокВариантовОтчета();
	
	СтрокиТекВарианта = СписокВариантовОтчета.НайтиСтроки(Новый Структура("КлючВариантаОтчета", КлючТекущихНастроек));
	Если СтрокиТекВарианта.Количество() > 0 тогда
		Элементы.СписокВариантовОтчета.ТекущаяСтрока = СтрокиТекВарианта[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВариантовОтчета(КлючВарианта = Неопределено)
	
	ВариантыОтчета = РеквизитФормыВЗначение("СписокВариантовОтчета");
	ВариантыОтчета.Очистить();
	ВариантыОтчета.Колонки.Добавить("ДатаИспользования");

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВариантыОтчета.Наименование КАК Наименование,
	|	ВариантыОтчета.Ссылка,
	|	ВариантыОтчета.КлючВарианта,
	|	ВариантыОтчета.Администратор,
	|	ВариантыОтчета.Администратор.Наименование КАК АдминистраторПредставление,
	|	ПОДСТРОКА(ВариантыОтчета.Описание, 0, 1000) КАК Описание
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчета
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов.Подсистемы КАК ВариантыОтчетаПодсистемы
	|	ПО ВариантыОтчетаПодсистемы.Ссылка = ВариантыОтчета.Ссылка
	|
	|ГДЕ
	|	ВариантыОтчета.КлючОбъекта = &КлючОбъекта
	|	И НЕ ВариантыОтчета.ПометкаУдаления
	|	И (НЕ ВариантыОтчетаПодсистемы.Подсистема ЕСТЬ NULL
	|		ИЛИ ВариантыОтчета.ТипВариантаОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыВариантовОтчетов.ВнешнийОтчет))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование
	|";

	Запрос.Параметры.Вставить("КлючОбъекта", КлючОбъекта);
	
 	УстановитьПривилегированныйРежим(истина);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
 	УстановитьПривилегированныйРежим(ложь);

	Пока Выборка.Следующий() Цикл
		СтрокаСписка = ВариантыОтчета.Добавить();
		СтрокаСписка.Наименование       = Выборка.Наименование;
		СтрокаСписка.КлючВариантаОтчета = Выборка.КлючВарианта;
		СтрокаСписка.Ссылка             = Выборка.Ссылка;
		СтрокаСписка.Автор              = Выборка.Администратор;
		СтрокаСписка.АвторПредставление = Выборка.АдминистраторПредставление;
		СтрокаСписка.Описание           = Выборка.Описание;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ВариантыОтчета, "СписокВариантовОтчета");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВариантовОтчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ЗагрузитьВыполнить();
КонецПроцедуры
