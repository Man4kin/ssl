
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Значение = ХранилищеОбщихНастроек.Загрузить("НастройкаОткрытияМакетов", "СпрашиватьРежимОткрытияМакета");
	
	Если Значение = Неопределено Тогда
		БольшеНеСпрашивать = Ложь;
	Иначе
		БольшеНеСпрашивать = НЕ Значение;
	КонецЕсли;
	
	Значение = ХранилищеОбщихНастроек.Загрузить("НастройкаОткрытияМакетов", "РежимОткрытияМакетаПросмотр");
	
	Если Значение = Неопределено Тогда
		КакОткрывать = 0;
	Иначе
		Если Значение Тогда
			КакОткрывать = 0;
		Иначе
			КакОткрывать = 1;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	СпрашиватьРежимОткрытияМакета = НЕ БольшеНеСпрашивать;
	РежимОткрытияМакетаПросмотр = ?(КакОткрывать = 0, Истина, Ложь);
	
	СохранитьНастройкиРежимаОткрытияМакета(СпрашиватьРежимОткрытияМакета, РежимОткрытияМакетаПросмотр);
	
	Закрыть(Новый Структура("БольшеНеСпрашивать, РежимОткрытияПросмотр",
							БольшеНеСпрашивать,
							РежимОткрытияМакетаПросмотр) );
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиРежимаОткрытияМакета(СпрашиватьРежимОткрытияМакета, РежимОткрытияМакетаПросмотр)
	
	ХранилищеОбщихНастроек.Сохранить("НастройкаОткрытияМакетов", "СпрашиватьРежимОткрытияМакета", СпрашиватьРежимОткрытияМакета);
	ХранилищеОбщихНастроек.Сохранить("НастройкаОткрытияМакетов", "РежимОткрытияМакетаПросмотр", РежимОткрытияМакетаПросмотр);
	
КонецПроцедуры
