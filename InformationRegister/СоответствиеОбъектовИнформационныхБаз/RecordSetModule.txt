
Процедура ПередЗаписью(Отказ, Замещение)
	
	// заполняем свойство УникальныйИдентификаторИсточникаСтрокой из ссылки источника
	Если Количество() > 0 Тогда
		ЭтотОбъект[0]["УникальныйИдентификаторИсточникаСтрокой"] = Строка(ЭтотОбъект[0]["УникальныйИдентификаторИсточника"].УникальныйИдентификатор());
	КонецЕсли;
	
	Если ОбменДанными.Загрузка
		ИЛИ Не ЗначениеЗаполнено(Отбор.УзелИнформационнойБазы.Значение)
		ИЛИ Не ОбщегоНазначения.СсылкаСуществует(Отбор.УзелИнформационнойБазы.Значение) Тогда
		Возврат;
	КонецЕсли;
	
	// отказываемся от выполнения типового механизма регистрации объектов
	ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
	
	// удаляем все узлы, добавленные по авторегистрации, если признак авторегистрации был ошибочно установлен
	ОбменДанными.Получатели.Очистить();
	
	// набор записей должен регистрироваться только на одном узле, указанном в отборе
	ОбменДанными.Получатели.Добавить(Отбор.УзелИнформационнойБазы.Значение);
	
КонецПроцедуры



