
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если Количество() = 1 Тогда
		Папка = Получить(0).Папка;
		Путь = Получить(0).Путь;
		
		Если ПустаяСтрока(Путь) Тогда
			Возврат;
		КонецЕсли;						
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПапкиФайлов.Ссылка,
			|	ПапкиФайлов.Наименование
			|ИЗ
			|	Справочник.ПапкиФайлов КАК ПапкиФайлов
			|ГДЕ
			|	ПапкиФайлов.Родитель = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Папка);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			РабочийКаталог = Путь;
			// Добавляем слэш в конце, если его нет
			Если Прав(РабочийКаталог,1) <> "\" Тогда
				РабочийКаталог = РабочийКаталог + "\";
			КонецЕсли;
			
			РабочийКаталог = РабочийКаталог + Выборка.Наименование + "\";
			
			РаботаСФайламиВызовСервера.СохранитьРабочийКаталог(Выборка.Ссылка, РабочийКаталог);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
