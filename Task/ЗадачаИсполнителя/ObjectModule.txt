
// Возвращает Истина, если в задаче указан исполнитель или роль исполнителя.
//
Функция РеквизитыАдресацииЗаполнены() Экспорт
	
	Возврат НЕ Исполнитель.Пустая() ИЛИ НЕ РольИсполнителя.Пустая();

КонецФункции

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗадачаБылаВыполнена = Ссылка.Выполнена;
	Если НЕ ЗадачаБылаВыполнена И Выполнена Тогда
		
		Если НЕ РеквизитыАдресацииЗаполнены() Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Необходимо указать исполнителя задачи.'"),,,
				"Объект.Исполнитель", Отказ);
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ЗадачаБылаВыполнена И Выполнена Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Эта задача уже была выполнена ранее.'"),,,, Отказ);
		Возврат;
	КонецЕсли;
	
	Если СрокИсполнения <> '00010101' И ДатаНачала > СрокИсполнения Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Дата начала исполнения не должна превышать крайний срок.'"),,,
			"Объект.ДатаНачала", Отказ);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	ЗадачаБылаВыполнена = Ложь;
	Если НЕ Ссылка.Пустая() Тогда
		ЗадачаБылаВыполнена = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Ссылка, "Выполнена");
	КонецЕсли;
	Если НЕ ЗадачаБылаВыполнена И Выполнена Тогда
		// Если задача выполнена, то запишем в реквизит Исполнитель того
		// пользователя, который фактически выполнил задачу. Это нам потом
		// потребуется для отчетов. Такую запись делаем только в том
		// случае, если в базе было не выполнено, а в объекте стало выполнено
		Если Исполнитель.Пустая() Тогда
			Исполнитель = ОбщегоНазначения.ТекущийПользователь();
		КонецЕсли;
		Если ДатаИсполнения = Дата(1,1,1) Тогда
			ДатаИсполнения = ТекущаяДата();
		КонецЕсли;
	КонецЕсли;
	
	Если Важность.Пустая() Тогда
		Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
	КонецЕсли;
	
	Если ПустаяСтрока(ПредметСтрокой) Тогда
		ПредметСтрокой = БизнесПроцессыИЗадачиСервер.ПредметСтрокой(Предмет);
	КонецЕсли;
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если НЕ Ссылка.Пустая() Тогда
		ПредыдущаяПометкаУдаления = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Ссылка, "ПометкаУдаления");
	КонецЕсли;
	Если ПредыдущаяПометкаУдаления <> ПометкаУдаления Тогда
		УстановитьПривилегированныйРежим(Истина);
		НачатьТранзакцию();
		Попытка
			ВложенныеБизнесПроцессы = БизнесПроцессыИЗадачиСервер.БизнесПроцессыВедущейЗадачи(Ссылка);
			Для Каждого ВложенныйБизнесПроцесс Из ВложенныеБизнесПроцессы Цикл
				БизнесПроцессОбъект = ВложенныйБизнесПроцесс.ПолучитьОбъект();
				БизнесПроцессОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
			КонецЦикла;	
			УстановитьПривилегированныйРежим(Ложь);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
	КонецЕсли;
	
	// УправлениеДоступом
	ГруппаИсполнителейЗадач = Справочники.ГруппыИсполнителейЗадач.ГруппаИсполнителейЗадач(РольИсполнителя, ОсновнойОбъектАдресации, ДополнительныйОбъектАдресации);
	// Конец УправлениеДоступом
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ЗадачаОбъект.ЗадачаИсполнителя") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, 
			"БизнесПроцесс,ТочкаМаршрута,Наименование,Исполнитель,РольИсполнителя,ОсновнойОбъектАдресации," + 
			"ДополнительныйОбъектАдресации,Важность,ДатаИсполнения,Автор,Описание,СрокИсполнения," + 
			"ДатаНачала,РезультатВыполнения,Предмет");
		Дата = ТекущаяДата();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Важность) Тогда
		Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
	КонецЕсли;
	
КонецПроцедуры


