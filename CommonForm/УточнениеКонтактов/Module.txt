
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	соотвАдреса = Новый Соответствие;

	Предмет = Параметры.Предмет;
	
	Для Каждого ЭлементСписка Из Параметры.СписокВыбранных Цикл
		
		Если ЭлементСписка.Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Стр Из ЭлементСписка.Значение Цикл
			
			стрПоиска = Стр.Адрес + Символы.ПС + Стр.Представление;
			Если соотвАдреса.Получить(стрПоиска) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			соотвАдреса.Вставить(стрПоиска, Истина);
			
			новСтр = Получатели.Добавить();
			новСтр.Адрес = Стр.Адрес;
			новСтр.Представление = Стр.Представление;
			новСтр.Контакт = Стр.Контакт;
			новСтр.ПолноеПредставление = УправлениеЭлектроннойПочтой.ПолучитьПредставлениеАдресата(Стр.Представление, Стр.Адрес, "");
		КонецЦикла;
		
	КонецЦикла;

	ЗаполнитьСпискиНайденныхКонтактов();

КонецПроцедуры

&НаКлиенте
Процедура КонтактНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	текДанные = Элементы.Получатели.ТекущиеДанные;
	Если ВзаимодействияКлиент.ВыбратьКонтакт(
			Предмет, текДанные.Адрес, текДанные.Представление, текДанные.Контакт, Истина, Ложь, Ложь
		) Тогда
		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КомандаОКВыполнить()
	
	Результат = Новый Соответствие;
	Для Каждого Стр Из Получатели Цикл
		Если ЗначениеЗаполнено(Стр.Контакт) Тогда
			Представление = УправлениеЭлектроннойПочтойКлиент.ПолучитьПолноеПредставлениеПоАдресуИПредставлению(Стр.Адрес, Стр.Представление);
			Результат.Вставить(Представление, Стр.Контакт);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиНайденныхКонтактов()
	
	// Получим список адресов, для которых не указан email
	масАдресов = Новый Массив;
	Для Каждого Стр Из Получатели Цикл
		Если Не ПустаяСтрока(Стр.Адрес) И Не ЗначениеЗаполнено(Стр.Контакт) Тогда
			масАдресов.Добавить(Стр.Адрес);
		КонецЕсли;
	КонецЦикла;
	
	// Если для всех адресов email указан, то поиск не осуществляем
	Если масАдресов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Найдем контакты по email
	НайденныеКонтакты = Взаимодействия.ПолучитьВсеКонтактыПоСпискуEmail(масАдресов);
	Если НайденныеКонтакты.Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Для каждой строки проставим список найденных контактов
	Для Каждого Стр Из Получатели Цикл
		Если Не ПустаяСтрока(Стр.Адрес) И Не ЗначениеЗаполнено(Стр.Контакт) Тогда
			Группа = НайденныеКонтакты.Строки.Найти(Стр.Адрес, "Представление");
			Если Группа = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого СтрГруппы Из Группа.Строки Цикл
				ПредставлениеКонтакта = СтрГруппы.Наименование + ?(ПустаяСтрока(СтрГруппы.НаименованиеВладельца), "", " (" + СтрГруппы.НаименованиеВладельца + ")");
				Стр.СписокНайденныхКонтактов.Добавить(СтрГруппы.Контакт, ПредставлениеКонтакта);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Элементы.Контакт.СписокВыбора.Очистить();
	Если Элемент.ТекущиеДанные.СписокНайденныхКонтактов.Количество() = 0 Тогда
		Элементы.Контакт.КнопкаСпискаВыбора = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.Контакт.КнопкаСпискаВыбора = Истина;
	Для Каждого ЭлементСписка Из Элемент.ТекущиеДанные.СписокНайденныхКонтактов Цикл
		Элементы.Контакт.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Элементы.Получатели.ТекущиеДанные.Контакт = ВыбранноеЗначение;

КонецПроцедуры
