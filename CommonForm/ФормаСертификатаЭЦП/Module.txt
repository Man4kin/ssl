
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураСертификата = Параметры.СтруктураСертификата;
	Отпечаток = Параметры.Отпечаток;
	
	КомуВыдан = СтруктураСертификата.КомуВыдан;
	КемВыдан = СтруктураСертификата.КемВыдан;
	ДействителенДо = СтруктураСертификата.ДействителенДо;
	Назначение = СтруктураСертификата.Назначение;
	
	НовоеНазначение = "";
	ПисатьКодНазначения = Истина;
	ОбщегоНазначения.ЗаполнитьНазначениеСертификата(Назначение, НовоеНазначение, ПисатьКодНазначения);
	Назначение = НовоеНазначение;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
	Если РасширениеПодключено Тогда
		
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		Фильтр = НСтр("ru = 'Все файлы(*.cer)|*.cer'");
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файл для сохранения сертификата'");
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			
			ДвоичныеДанныеОтпечатка = Base64Значение(Отпечаток);
			
			МенеджерКриптографии = ЭлектронноЦифроваяПодписьКлиент.ПолучитьМенеджерКриптографии();

			ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов();
			Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
			
			Если Сертификат = Неопределено Тогда
				Отказ = Истина;
				Сообщить(НСтр("ru = 'Сертификат не найден'"));
				Возврат;
			КонецЕсли;
			
			Сертификат.Выгрузить(ДиалогОткрытияФайла.ПолноеИмяФайла);
			
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Сертификат ЭЦП сохранен в файл ""%1""'"),
				ДиалогОткрытияФайла.ПолноеИмяФайла);
			Состояние(Текст);
			
		КонецЕсли;
		
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Процедура Проверить(Команда)
	
	ДвоичныеДанныеОтпечатка = Base64Значение(Отпечаток);
	
	МенеджерКриптографии = ЭлектронноЦифроваяПодписьКлиент.ПолучитьМенеджерКриптографии();

	ХранилищеСертификатов = МенеджерКриптографии.ПолучитьХранилищеСертификатов();
	Сертификат = ХранилищеСертификатов.НайтиПоОтпечатку(ДвоичныеДанныеОтпечатка);
	
	Если Сертификат = Неопределено Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю((НСтр("ru = 'Сертификат не найден'")));
		Возврат;
	КонецЕсли;
	
	Попытка
		МенеджерКриптографии.ПроверитьСертификат(Сертификат);
		Предупреждение(НСтр("ru = 'Сертификат действителен'"));
	Исключение
		Текст = НСтр("ru = 'Сертификат не действителен.'") + Символы.ПС + КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Предупреждение(Текст);
	КонецПопытки;	
	
КонецПроцедуры
