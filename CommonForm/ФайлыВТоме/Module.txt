&НаСервере
Процедура УстановитьЗапросДинамическогоСписка(ИмяХранилища)
	
	ТекстЗапроса = ШаблонЗапроса();
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ИмяСправочника]", ИмяХранилища);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ЭтоПрисоединенныеФайлы]", ?(ИмяХранилища="ВерсииФайлов", "Ложь", "Истина"));
	
	ФайлыВТомах.ТекстЗапроса = ТекстЗапроса;
	ФайлыВТомах.ДинамическоеСчитываниеДанных = Истина;
	ФайлыВТомах.Параметры.УстановитьЗначениеПараметра("Том", Том);
	ФайлыВТомах.УстановитьОбязательноеИспользование("Ссылка", Истина);
	ФайлыВТомах.УстановитьОбязательноеИспользование("ЭтоПрисоединенныеФайлы", Истина);
	
КонецПроцедуры

&НаСервере
Функция ШаблонЗапроса()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ХранилищеФайлов.Ссылка			КАК Ссылка,
	|	ХранилищеФайлов.ИндексКартинки	КАК ИндексКартинки,
	|	ХранилищеФайлов.ПутьКФайлу		КАК ПутьКФайлу,
	|	ХранилищеФайлов.Размер			КАК Размер,
	|	ХранилищеФайлов.Автор			КАК Автор,
	|	ХранилищеФайлов.Том				КАК Том,
	|	[ЭтоПрисоединенныеФайлы]		КАК ЭтоПрисоединенныеФайлы
	|ИЗ
	|	Справочник.[ИмяСправочника] КАК ХранилищеФайлов
	|ГДЕ
	|	ХранилищеФайлов.Том = &Том";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ПолучитьИменаХранилищФайлов()
	
	Для Каждого СправочникОМ Из Метаданные.Справочники Цикл
		Если СправочникОМ.Имя = "ВерсииФайлов"
		 ИЛИ Прав(СправочникОМ.Имя, 19) = "ПрисоединенныеФайлы" Тогда
			ИменаХранилищФайлов.Добавить(СправочникОМ.Имя, СправочникОМ.Синоним);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИменаХранилищФайлов;
	
КонецФункции

&НаСервере
Процедура ДобавитьПолеВСписок(Контейнер, ИмяРеквизита, Заголовок = "", Видимость = Истина)
	
	Элемент = Элементы.Добавить("ФайлыВТомах" + ИмяРеквизита, Тип("ПолеФормы"), Контейнер);
	Элемент.ПутьКДанным = "ФайлыВТомах." + ИмяРеквизита;
	Элемент.Заголовок = Заголовок;
	Элемент.Видимость = Видимость;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Том = Параметры.Том;
	
	// Получаем доступные хранилища файлов
	ПолучитьИменаХранилищФайлов();
	
	Если ИменаХранилищФайлов.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Не найдены хранилища файлов!'");
	ИначеЕсли ИменаХранилищФайлов.Количество() = 1 Тогда
		Элементы.ТекущееХранилищеПредставление.Видимость = Ложь;
	КонецЕсли;
	
	ИмяХранилища = ХранилищеОбщихНастроек.Загрузить("ОбщаяФорма.ФайлыВТоме.ОтборПоХранилищам", Строка(Том.УникальныйИдентификатор()));
	
	Если ИмяХранилища = Неопределено
	 Или ИменаХранилищФайлов.НайтиПоЗначению(ИмяХранилища) = Неопределено Тогда
	
		ЭлементВерсииФайлов = ИменаХранилищФайлов.НайтиПоЗначению("ВерсииФайлов");
		
		Если ЭлементВерсииФайлов = Неопределено Тогда
			ИмяОбъектаВладельцаФайла = ИменаХранилищФайлов[0].Значение;
			ВладелецФайлов = ИменаХранилищФайлов[0].Представление;
		Иначе
			ИмяОбъектаВладельцаФайла = ЭлементВерсииФайлов.Значение;
			ВладелецФайлов = ЭлементВерсииФайлов.Представление;
		КонецЕсли;
		
	Иначе
		ИмяОбъектаВладельцаФайла = ИмяХранилища;
		ВладелецФайлов = ИменаХранилищФайлов.НайтиПоЗначению(ИмяХранилища).Представление;
	КонецЕсли;
	
	УстановитьЗапросДинамическогоСписка(ИмяОбъектаВладельцаФайла);
	
	Элементы.ФайлыВТомах.КартинкаСтрок = БиблиотекаКартинок.ПиктограммыФайлов;
	Элементы.ФайлыВТомах.ПутьКДаннымКартинкиСтроки = "ФайлыВТомах.ИндексКартинки";
	
	ДобавитьПолеВСписок(Элементы.ФайлыВТомах, "ПутьКФайлу",	НСтр("ru = 'Путь к файлу'"));
	ДобавитьПолеВСписок(Элементы.ФайлыВТомах, "Размер",		НСтр("ru = 'Размер'"));
	ДобавитьПолеВСписок(Элементы.ФайлыВТомах, "Автор",		НСтр("ru = 'Автор'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если СохранитьНастройкиОтбораХранилищ Тогда
		СохранитьНастройкиОтбора(Том, ИмяОбъектаВладельцаФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиОтбора(Том, ТекущиеНастройки)
	
	ХранилищеОбщихНастроек.Сохранить("ОбщаяФорма.ФайлыВТоме.ОтборПоХранилищам", Строка(Том.УникальныйИдентификатор()), ТекущиеНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	
	ОткрытьКарточкуФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыВТомахВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьКарточкуФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуФайла()
	
	ТекущиеДанные = Элементы.ФайлыВТомах.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ЭтоПрисоединенныеФайлы Тогда
		ПрисоединенныеФайлыКлиент.ОткрытьФормуПрисоединенногоФайла(ТекущиеДанные.Ссылка);
	Иначе
		ОткрытьЗначение(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецФайловНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущееХранилище = ИменаХранилищФайлов.НайтиПоЗначению(ИмяОбъектаВладельцаФайла);
	
	ТекущееХранилище = ВыбратьИзСписка(ИменаХранилищФайлов, Элементы.ВладелецФайлов, ТекущееХранилище);
	
	Если ТипЗнч(ТекущееХранилище) = Тип("ЭлементСпискаЗначений") Тогда
		ИмяОбъектаВладельцаФайла = ТекущееХранилище.Значение;
		ВладелецФайлов = ТекущееХранилище.Представление;
		УстановитьЗапросДинамическогоСписка(ИмяОбъектаВладельцаФайла);
		СохранитьНастройкиОтбораХранилищ = Истина;
	КонецЕсли;
	
КонецПроцедуры
