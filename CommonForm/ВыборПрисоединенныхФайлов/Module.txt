&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецФайла = Параметры.ВладелецФайла;
	ЗакрыватьПриВыборе = Параметры.ЗакрыватьПриВыборе;
	
	УстановитьСвойстваДинамическогоСписка();
	УстановитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СтандартаняКомандаВыбрать(Команда)
	ОбработкаВыбораЗначения();
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработкаВыбораЗначения()
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработкаВыбораЗначения()
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораЗначения()
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	ОповеститьОВыборе(ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СтандартаняКомандаСвойстваОбъекта(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ПрисоединенныйФайл", Элементы.Список.ТекущиеДанные.Ссылка);
	ОткрытьФорму("ОбщаяФорма.ПрисоединенныйФайл", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваДинамическогоСписка()
	
	ИмяСправочникаХранилищаФайлов = СтрЗаменить("[ИмяОбъекта]ПрисоединенныеФайлы", "[ИмяОбъекта]",
				СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ВладелецФайла.Метаданные().ПолноеИмя(), ".")[1]);
	
	ТекстЗапроса = ШаблонЗапроса();
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ИмяСправочника]", ИмяСправочникаХранилищаФайлов);
	
	Список.ТекстЗапроса = ТекстЗапроса;
	Список.ДинамическоеСчитываниеДанных = Истина;
	
	Список.Параметры.УстановитьЗначениеПараметра("ВладелецФайла", ВладелецФайла);
	Список.ОсновнаяТаблица = "Справочник." + ИмяСправочникаХранилищаФайлов;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЭлементыФормы()
	
	ЭлементТаблица = Элементы.Список;
	
	ЭлементТаблица.КартинкаСтрок = БиблиотекаКартинок.ПиктограммыФайлов;
	ЭлементТаблица.ПутьКДаннымКартинкиСтроки = "Список.ИндексКартинки";
	ЭлементТаблица.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	
	ДобавитьПолеВСписок(ЭлементТаблица, "Ссылка", "Наименование");
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолеВСписок(Контейнер, ИмяРеквизита, Заголовок = "")
	
	Элемент = Элементы.Добавить("Список" + ИмяРеквизита, Тип("ПолеФормы"), Контейнер);
	Элемент.ПутьКДанным = "Список." + ИмяРеквизита;
	
	Если Не ПустаяСтрока(Заголовок) Тогда
		Элемент.Заголовок = Заголовок;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ШаблонЗапроса()
	
	ШаблонЗапроса = "ВЫБРАТЬ
					|	Файлы.Ссылка,
					|	ВЫБОР
					|		КОГДА Файлы.ПометкаУдаления = ИСТИНА
					|			ТОГДА Файлы.ИндексКартинки + 1
					|		ИНАЧЕ Файлы.ИндексКартинки
					|	КОНЕЦ КАК ИндексКартинки
					|ИЗ
					|	Справочник.[ИмяСправочника] КАК Файлы
					|ГДЕ
					|	Файлы.ВладелецФайла = &ВладелецФайла";
	
	Возврат ШаблонЗапроса;
	
КонецФункции
