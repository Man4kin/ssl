&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивСтруктурСертификатов = Параметры.МассивСтруктурСертификатов;
	
	Если Параметры.Свойство("ОбъектСсылка") Тогда
		ОбъектСсылка = Параметры.ОбъектСсылка;
	КонецЕсли;	
	
	Заголовок = НСтр("ru = 'Подписать ""'") + Строка(ОбъектСсылка) + """";
	
	Для Каждого СтруктураСертификата Из МассивСтруктурСертификатов Цикл
		НоваяСтрока = ТаблицаСертификатов.Добавить();
		
		НоваяСтрока.КомуВыдан = СтруктураСертификата.КомуВыдан;
		НоваяСтрока.КемВыдан = СтруктураСертификата.КемВыдан;
		НоваяСтрока.ДействителенДо = СтруктураСертификата.ДействителенДо;
		НоваяСтрока.Отпечаток = СтруктураСертификата.Отпечаток;
		
		НовоеНазначение = "";
		ОбщегоНазначения.ЗаполнитьНазначениеСертификата(СтруктураСертификата.Назначение, НовоеНазначение);
		НоваяСтрока.Назначение = НовоеНазначение;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСертификатовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработкаВыбора();
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	ОбработкаВыбора();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора()
	
	ТекущиеДанные = Элементы.ТаблицаСертификатов.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда 
		
		ВозвратПароль = Пароль;
		
		Если ПустаяСтрока(Пароль) И НЕ ПустаяСтрока(Элементы.Пароль.ТекстРедактирования) Тогда
			ВозвратПароль = Элементы.Пароль.ТекстРедактирования;
		КонецЕсли;	
		
		ВыбранныйСертификат = ЭлектронноЦифроваяПодписьКлиент.ПолучитьСертификатПоОтпечатку(ТекущиеДанные.Отпечаток);
		СтруктураВозврата = Новый Структура("Сертификат, Комментарий, Пароль", 
			ВыбранныйСертификат, Комментарий, ВозвратПароль);
			
		Закрыть(СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификат(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаСертификатов.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда 
		
		ВыбранныйСертификат = ЭлектронноЦифроваяПодписьКлиент.ПолучитьСертификатПоОтпечатку(ТекущиеДанные.Отпечаток);
		
		СтруктураСертификата = ЭлектронноЦифроваяПодписьКлиент.ЗаполнитьСтруктуруСертификата(ВыбранныйСертификат);
		Если СтруктураСертификата <> Неопределено Тогда
			ПараметрыФормы = Новый Структура("СтруктураСертификата, Отпечаток", СтруктураСертификата, ТекущиеДанные.Отпечаток);
			СтруктураВозврата = ОткрытьФормуМодально("Общаяформа.ФормаСертификатаЭЦП", ПараметрыФормы);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
