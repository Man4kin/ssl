&НаСервере
Процедура ОбновитьПараметрыНаСервере()
	
	УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ЛокальныйКэшФайлов", "УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования");
	Если УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования = Неопределено Тогда
		УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования = Ложь;
	КонецЕсли;
	
	ПодтверждатьПриУдаленииИзЛокальногоКэшаФайлов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ЛокальныйКэшФайлов", "ПодтверждатьПриУдаленииИзЛокальногоКэшаФайлов");
	Если ПодтверждатьПриУдаленииИзЛокальногоКэшаФайлов = Неопределено Тогда
		ПодтверждатьПриУдаленииИзЛокальногоКэшаФайлов = Ложь;
	КонецЕсли;
	
	МаксРазмер = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ЛокальныйКэшФайлов", "МаксимальныйРазмерЛокальногоКэшаФайлов");
	Если МаксРазмер = Неопределено Тогда
		МаксРазмер = 100*1024*1024; // 100 мб		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ЛокальныйКэшФайлов", "МаксимальныйРазмерЛокальногоКэшаФайлов", МаксРазмер);
	КонецЕсли;
	МаксимальныйРазмерЛокальногоКэшаФайлов = МаксРазмер / 1048576;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметрыНаКлиенте()
	РаботаСФайламиКлиент.ПроинициализироватьПутьКРабочемуКаталогу();
	ПутьКЛокальномуКэшуФайлов = РаботаСФайламиКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами().ПутьКЛокальномуКэшуФайлов;
	
	#Если НЕ ВебКлиент Тогда
		МассивФайлов = НайтиФайлы(ПутьКЛокальномуКэшуФайлов, "*.*");
		РазмерФайловВРабочемКаталоге = 0;
		КоличествоСуммарное = 0;
		ФайловыеФункцииКлиент.ОбходФайловРазмер(ПутьКЛокальномуКэшуФайлов, МассивФайлов, РазмерФайловВРабочемКаталоге, КоличествоСуммарное); 
		
		РазмерФайловВРабочемКаталоге = РазмерФайловВРабочемКаталоге / 1048576;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметры()
	ОбновитьПараметрыНаСервере();
	ОбновитьПараметрыНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВыполнить()
	МассивСтруктур = Новый Массив;
	
	Элемент = Новый Структура;
	Элемент.Вставить("Объект", "ЛокальныйКэшФайлов");
	Элемент.Вставить("Настройка", "ПутьКЛокальномуКэшуФайлов");
	Элемент.Вставить("Значение", ПутьКЛокальномуКэшуФайлов);
	МассивСтруктур.Добавить(Элемент);
	
	Элемент = Новый Структура;
	Элемент.Вставить("Объект", "ЛокальныйКэшФайлов");
	Элемент.Вставить("Настройка", "МаксимальныйРазмерЛокальногоКэшаФайлов");
	Элемент.Вставить("Значение", МаксимальныйРазмерЛокальногоКэшаФайлов * 1048576);
	МассивСтруктур.Добавить(Элемент);
	
	Элемент = Новый Структура;
	Элемент.Вставить("Объект", "ЛокальныйКэшФайлов");
	Элемент.Вставить("Настройка", "УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования");
	Элемент.Вставить("Значение", УдалятьФайлИзЛокальногоКэшаФайловПриЗавершенииРедактирования);
	МассивСтруктур.Добавить(Элемент);
	
	Элемент = Новый Структура;
	Элемент.Вставить("Объект", "ЛокальныйКэшФайлов");
	Элемент.Вставить("Настройка", "ПодтверждатьПриУдаленииИзЛокальногоКэшаФайлов");
	Элемент.Вставить("Значение", ПодтверждатьПриУдаленииИзЛокальногоКэшаФайлов);
	МассивСтруктур.Добавить(Элемент);
	
	РаботаСФайлами.ХранилищеОбщихНастроекСохранитьМассивИПараметрСеансаРабочийКаталог(МассивСтруктур, ПутьКЛокальномуКэшуФайлов);
	
	ОбновитьПовторноИспользуемыеЗначения();
	РаботаСФайламиКлиент.ПроинициализироватьПутьКРабочемуКаталогу();
	
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
	Если НЕ РасширениеПодключено Тогда
		Предупреждение(НСтр("ru = 'В Веб-клиенте без установленного расширения работы с файлами настройка рабочего каталога не поддерживается.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОбновитьПараметрыНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловВыполнить()
	ОткрытьФормуМодально("Справочник.Файлы.Форма.СписокФайловВЛокальномКэшеФайлов");
	ПутьКЛокальномуКэшуФайловВФорме = ПутьКЛокальномуКэшуФайлов;
	ОбновитьПараметры();
	ПутьКЛокальномуКэшуФайлов = ПутьКЛокальномуКэшуФайловВФорме;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВыполнить()
	ОбновитьПараметры();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьПараметрыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЛокальныйКэшФайлов(Команда)
	#Если НЕ ВебКлиент Тогда
	
		ТекстВопроса = НСтр("ru = 'Из основного рабочего каталога будут удалены все файлы, кроме занятых Вами для редактирования. Продолжить?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
		    Возврат;
		КонецЕсли;
		
		Состояние(НСтр("ru = 'Выполняется очистка основного рабочего каталога... 
						|Пожалуйста, подождите.'"));
		ИмяКаталога = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ЛокальныйКэшФайлов", "ПутьКЛокальномуКэшуФайлов");
		
		МассивФайлов = НайтиФайлы(ИмяКаталога, "*.*");
		
		РазмерФайловВРабочемКаталоге = 0;
		КоличествоСуммарное = 0;
		ФайловыеФункцииКлиент.ОбходФайловРазмер(ИмяКаталога, МассивФайлов, РазмерФайловВРабочемКаталоге, КоличествоСуммарное);
		
		ФайловыеФункцииКлиент.ОчиститьРабочийКаталог(РазмерФайловВРабочемКаталоге, 0, Истина); // ОчищатьВсе= Истина
		
		РазмерФайловВРабочемКаталоге = РазмерФайловВРабочемКаталоге / 1048576;
		
		ПутьКЛокальномуКэшуФайловВФорме = ПутьКЛокальномуКэшуФайлов;
		ОбновитьПараметры();
		ПутьКЛокальномуКэшуФайлов = ПутьКЛокальномуКэшуФайловВФорме;
		Состояние(НСтр("ru = 'Очистка основного рабочего каталога успешно завершена!'"));
	
	#КонецЕсли
КонецПроцедуры

// есть ли файлы на редактирование в основном рабочем каталоге
&НаКлиенте
Функция ЕстьФайлыНаРедактированиеВЛокальномКеше()
	СписокФайловВРегистре = РаботаСФайлами.СписокФайловВРегистре();
	
	Для Каждого Строка Из СписокФайловВРегистре Цикл
		
		Если Не Строка.НаЧтение Тогда
			ПолныйПуть = ПутьКЛокальномуКэшуФайлов + Строка.ЧастичныйПуть;
			Файл = Новый Файл(ПолныйПуть);
			Если Файл.Существует() Тогда
				Возврат Истина;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;		
	
	Возврат Ложь;
КонецФункции	

// вернет Истина, если можно продолжать (смену пути к рабочему каталогу)
&НаКлиенте
Функция ВопросПроФайлыНаРедактированиеВЛокальномКеше()
	Если ЕстьФайлыНаРедактированиеВЛокальномКеше() Тогда
		КодВозврата = Вопрос(Нстр("ru = 'В основном рабочем каталоге есть файлы на редактирование. Смена пути к рабочему каталогу приведет к потере изменений в этих файлах. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
		Если КодВозврата = КодВозвратаДиалога.Нет Тогда
			Возврат Ложь;
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат Истина;
КонецФункции	

&НаКлиенте
Процедура ПутьКЛокальномуКэшуФайловНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	РасширениеПодключено = ПодключитьРасширениеРаботыСФайлами();
	Если РасширениеПодключено Тогда
		
		Если Не ВопросПроФайлыНаРедактированиеВЛокальномКеше() Тогда
			Возврат;
		КонецЕсли;	
		
		Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
		ДиалогОткрытияФайла.ПолноеИмяФайла = "";
		ДиалогОткрытияФайла.Каталог = ПутьКЛокальномуКэшуФайлов;
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите путь к основному рабочему каталогу'");
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			
			ИмяКаталога = ДиалогОткрытияФайла.Каталог;
			ФайловыеФункцииКлиентСервер.ДобавитьСлешЕслиНужно(ИмяКаталога);
			
			// Создать каталог для файлов
			Попытка
				СоздатьКаталог(ИмяКаталога);
				ИмяКаталогаТестовое = ИмяКаталога + "ПроверкаДоступа\";
				СоздатьКаталог(ИмяКаталогаТестовое);
				УдалитьФайлы(ИмяКаталогаТестовое);
			Исключение
				// нет прав на создание каталога, или такой путь отсутствует
				
				ТекстОшибки 
					= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Неверный путь или отсутствуют права на запись в каталог ""%1""'"),
					ИмяКаталога);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "ПутьКЛокальномуКэшуФайлов");
				Возврат;
			КонецПопытки;
			
			ПутьКЛокальномуКэшуФайлов = ИмяКаталога;
		КонецЕсли;
	
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура ПутьКРабочемуКаталогуПоУмолчанию(Команда)
	ПутьКЛокальномуКэшуФайловВременный = ФайловыеФункцииКлиент.ПолучитьПутьККаталогуДанныхПользователя();
	
	Если ПутьКЛокальномуКэшуФайлов = ПутьКЛокальномуКэшуФайловВременный Тогда
		Возврат;
	КонецЕсли;	
	
	Если Не ВопросПроФайлыНаРедактированиеВЛокальномКеше() Тогда
		Возврат;
	КонецЕсли;	
	
	ПутьКЛокальномуКэшуФайлов = ПутьКЛокальномуКэшуФайловВременный;
КонецПроцедуры
