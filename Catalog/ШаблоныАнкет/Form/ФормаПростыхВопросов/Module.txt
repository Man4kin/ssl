&НаКлиенте
Перем ЗначенияПеречисленияТипыВопросовШаблонаАнкеты;
&НаКлиенте
Перем ЗначенияПеречисленияТипыОтветовНаВопрос;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ  

// Формирует структуру параметров для передачи в форму владельца
&НаКлиенте
Функция СформироватьСтруктуруПараметровДляПередачиВладельцу()

	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Обязательный",Обязательный);
	СтруктураВозврата.Вставить("Формулировка",Формулировка);
	СтруктураВозврата.Вставить("ЭлементарныйВопрос",ЭлементарныйВопрос);
	СтруктураВозврата.Вставить("ВысотаЭлементаФормулировкиВопроса",ВысотаЭлементаФормулировкиВопроса);
	
	Возврат СтруктураВозврата;

КонецФункции //

// Получает значение формулировки элементарного вопроса и заполняет список выбора поля Формулировка
&НаКлиенте
Функция СформироватьАвтоФормулировку(ФормулировкаЭлементарногоВопроса)

	Элементы.Формулировка.СписокВыбора.Очистить();
	
	Если ПустаяСтрока(Формулировка) Тогда
	    Формулировка = ФормулировкаЭлементарногоВопроса;	
	КонецЕсли;
	
	Элементы.Формулировка.СписокВыбора.Добавить(ФормулировкаЭлементарногоВопроса);
	
	Возврат ФормулировкаЭлементарногоВопроса;

КонецФункции // СформироватьАвтонаименование()

// Процедура получает необходмые значения предопределенных значений,
// необходимых на клиенте.
&НаСервере
Процедура ПолучениеПредопределенныхЗначенийНеобходимыхНаКлиенте()
    	
	ПеречислениеТипыВопросовШаблонаАнкеты = Анкетирование.ПолучитьСтруктуруЗначенияПеречисления("ТипыВопросовШаблонаАнкеты",Истина);
	ПредопределеныеЗначенияНеобходимыеНаКлиенте.Вставить(0,ПеречислениеТипыВопросовШаблонаАнкеты);
	
	ПеречислениеТипыОтветовНаВопрос = Анкетирование.ПолучитьСтруктуруЗначенияПеречисления("ТипыОтветовНаВопрос");
	ПредопределеныеЗначенияНеобходимыеНаКлиенте.Вставить(1,ПеречислениеТипыОтветовНаВопрос);
                                                                         
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма,Параметры);
	Если ТипСтрокиДерева = "Раздел" Тогда
		
		Элементы.ГруппаВысотаОбязательный.Видимость = Ложь;
		Элементы.ЭлементарныйВопрос.Видимость		= Ложь;
		Элементы.Формулировка.Заголовок 			= "Имя раздела";
		Заголовок									= "Раздел шаблона анкеты";
		
	КонецЕсли;
	
	ПолучениеПредопределенныхЗначенийНеобходимыхНаКлиенте();
	
	Если НЕ ЭлементарныйВопрос.Пустая() Тогда
		Элементы.Формулировка.СписокВыбора.Добавить(ОбщегоНазначения.ПолучитьЗначениеРеквизита(ЭлементарныйВопрос,"Формулировка"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗначенияПеречисленияТипыВопросовШаблонаАнкеты 	= ПредопределеныеЗначенияНеобходимыеНаКлиенте.Получить(0).Значение;
	ЗначенияПеречисленияТипыОтветовНаВопрос			= ПредопределеныеЗначенияНеобходимыеНаКлиенте.Получить(1).Значение;
	
КонецПроцедуры 

//////////////////////////////////////////////////////////
// КОМАНДЫ ФОРМЫ 

&НаКлиенте
Процедура ПеренестиВШаблон(Команда)
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(Формулировка) Тогда
		 Отказ = Истина;
		 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = ' Не заполнена формулировка'"),,"Формулировка");
	КонецЕсли;
	 
	Если ТипСтрокиДерева = "Вопрос" И (Не ЗначениеЗаполнено(ЭлементарныйВопрос)) Тогда
	    Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = ' Не указан элементарный вопрос'"),,"ЭлементарныйВопрос");	
	КонецЕсли; 
	 	
	Если Отказ Тогда
		Возврат;	
	КонецЕсли; 	
	
	Оповестить("ОкочаниеРедактированияПараметровСтрокиШаблонаАнкеты",СформироватьСтруктуруПараметровДляПередачиВладельцу());
	Закрыть();
	
КонецПроцедуры
 
&НаКлиенте
Процедура ЭлементарныйВопросНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
							
	Если ТипВопроса = ЗначенияПеречисленияТипыВопросовШаблонаАнкеты.ВопросСУсловием Тогда
		
		СтандартнаяОбработка = ЛОЖЬ;
		
		МассивОтбора = Новый Массив;
		МассивОтбора.Добавить(АнкетированиеКлиент.СоздатьСтруктуруПараметраОтбора(Тип("ЭлементОтбораКомпоновкиДанных"),"ТипОтвета",ВидСравненияКомпоновкиДанных.Равно,ЗначенияПеречисленияТипыОтветовНаВопрос.Булево));
		МассивОтбора.Добавить(АнкетированиеКлиент.СоздатьСтруктуруПараметраОтбора(Тип("ЭлементОтбораКомпоновкиДанных"),"ПометкаУдаления",ВидСравненияКомпоновкиДанных.Равно,ЛОЖЬ));
			
		ОткрытьФорму("ПланВидовХарактеристик.ВопросыДляАнкетирования.Форма.ФормаВыбора",Новый Структура("МассивОтбора",МассивОтбора),Элемент);
			
	КонецЕсли; 
	
КонецПроцедуры
 
&НаКлиенте
Процедура ЭлементарныйВопросОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РеквизитыВопрос = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ВыбранноеЗначение,"ЭтоГруппа,ТипОтвета,Формулировка");
	
	Если РеквизитыВопрос.ЭтоГруппа Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;	
	
	Если ТипВопроса = ЗначенияПеречисленияТипыВопросовШаблонаАнкеты.ВопросСУсловием 
		И РеквизитыВопрос.ТипОтвета <> ЗначенияПеречисленияТипыОтветовНаВопрос.Булево Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	СформироватьАвтоФормулировку(РеквизитыВопрос.Формулировка);
	
КонецПроцедуры

