////////////////////////////////////////////////////////////////////////////////
// Процедуры - обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокВыбранных.ЗагрузитьЗначения(Параметры.МасВыбранных);
	
	ЭтоДополнительноеСведение = Параметры.ЭтоДополнительноеСведение;
	
	Если ЭтоДополнительноеСведение Тогда
		Заголовок = НСтр("ru = 'Подбор дополнительных сведений'");
	Иначе
		Заголовок = НСтр("ru = 'Подбор дополнительных реквизитов'");
	КонецЕсли;
	
	ДеревоСвойств.Параметры.УстановитьЗначениеПараметра("масВыбранных", Параметры.МасВыбранных);
	
	ЭлементОтбора = ДеревоСвойств.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтоДополнительноеСведение");
	
	Если ЭтоДополнительноеСведение Тогда
		ЭлементОтбора.ПравоеЗначение = Истина;
	Иначе
		ЭлементОтбора.ПравоеЗначение = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьКомментарий();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписаноДополнительноеСвойство" Тогда
		
		Элементы.ДеревоСвойств.Обновить();
		
		ТекущиеДанные = Элементы.ДеревоСвойств.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И ТекущиеДанные.Ссылка = Источник Тогда
			Комментарий = Параметр.Комментарий;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзменилсяНаборВыбранных" И Источник = ЭтаФорма.ВладелецФормы Тогда
		Для Каждого Элемент Из Параметр.МассивСвойств Цикл
			Найденное = СписокВыбранных.НайтиПоЗначению(Элемент);
			Если Параметр.Операция = "Добавление" И Найденное = Неопределено Тогда
				СписокВыбранных.Добавить(Элемент);
			ИначеЕсли Параметр.Операция = "Удаление" И Найденное <> Неопределено Тогда
				СписокВыбранных.Удалить(Найденное);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДеревоСвойств.Параметры.УстановитьЗначениеПараметра("масВыбранных", СписокВыбранных.ВыгрузитьЗначения());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Процедуры - обработчики событий табличной части формы

&НаКлиенте
Процедура ДеревоСвойствПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	// Для групп оставим стандартную обработку
	Если Группа = Истина Тогда
		Возврат;
	ИначеЕсли Копирование И (Элементы.ДеревоСвойств.ТекущиеДанные.НомерКартинкиСведения % 3 = 0) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("ОткрываетсяИзНабораСвойств", Истина);
	
	Если Копирование Тогда
		ПараметрыОткрытия.Вставить("Копируемый", Элементы.ДеревоСвойств.ТекущаяСтрока);
	Иначе
		ПараметрыОткрытия.Вставить("Родитель", Родитель);
		ПараметрыОткрытия.Вставить("ЭтоДополнительноеСведение", ЭтоДополнительноеСведение);
	КонецЕсли;
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.ФормаЭлемента", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвойствПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	Если Элементы.ДеревоСвойств.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если (Элементы.ДеревоСвойств.ТекущиеДанные.НомерКартинкиСведения % 3 = 0) Тогда
		ИмяФормыПВХ = "ФормаГруппы";
	Иначе
		ИмяФормыПВХ = "ФормаЭлемента";
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", Элементы.ДеревоСвойств.ТекущаяСтрока);
	ПараметрыОткрытия.Вставить("ОткрываетсяИзНабораСвойств", Истина);
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма." + ИмяФормыПВХ, ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвойствПриАктивизацииСтроки(Элемент)
	
	УстановитьКомментарий();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвойствВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыполнитьПодборСвойств();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСвойствоВНабор(Команда)
	
	ВыполнитьПодборСвойств();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодборСвойств()
	
	Если Элементы.ДеревоСвойств.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МасДобавляемыеСвойства = Новый Массив;
	
	Для Каждого ВыделенаяСтрока ИЗ Элементы.ДеревоСвойств.ВыделенныеСтроки Цикл
		Свойство = Элементы.ДеревоСвойств.ДанныеСтроки(ВыделенаяСтрока).Ссылка;
		МасДобавляемыеСвойства.Добавить(Свойство);
	КонецЦикла;
	
	Результат = Новый Структура("ЭтоДополнительноеСведение, МассивДобавляемых",
								ЭтоДополнительноеСведение,
								МасДобавляемыеСвойства);
	
	Оповестить("ПодборСвойствВНабор", Результат);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Служебные процедуры и функции

&НаКлиенте
Процедура УстановитьКомментарий()
	
	Если Элементы.ДеревоСвойств.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Комментарий = Элементы.ДеревоСвойств.ТекущиеДанные.Комментарий;
	
КонецПроцедуры
