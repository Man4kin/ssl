&НаСервере
Процедура ЗаполнитьСписокПодписейОдногоОбъекта(ТекущийОбъект)
	
	Если ТекущийОбъект.Подписан Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ЭлектронноЦифровыеПодписи.КомуВыданСертификат КАК КомуВыданСертификат,
		               |	ЭлектронноЦифровыеПодписи.ДатаПодписи КАК ДатаПодписи,
		               |	ЭлектронноЦифровыеПодписи.Комментарий КАК Комментарий,
		               |	ЭлектронноЦифровыеПодписи.Подпись КАК Подпись,
		               |	ЭлектронноЦифровыеПодписи.Отпечаток КАК Отпечаток,
		               |	ЭлектронноЦифровыеПодписи.УстановившийПодпись КАК УстановившийПодпись,
		               |	ЭлектронноЦифровыеПодписи.НомерСтроки КАК НомерСтроки,
		               |	ЭлектронноЦифровыеПодписи.ИмяФайлаПодписи КАК ИмяФайлаПодписи
		               |ИЗ
		               |	Справочник.ВерсииФайлов.ЭлектронноЦифровыеПодписи КАК ЭлектронноЦифровыеПодписи
		               |ГДЕ
		               |	ЭлектронноЦифровыеПодписи.Ссылка = &ОбъектСсылка";
					   
		Запрос.Параметры.Вставить("ОбъектСсылка", ТекущийОбъект);
		ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаЗапроса.Следующий() Цикл
			НоваяСтрока = ТаблицаПодписей.Добавить();
			НоваяСтрока.КомуВыданСертификат = ВыборкаЗапроса.КомуВыданСертификат;
			НоваяСтрока.ДатаПодписи = ВыборкаЗапроса.ДатаПодписи;
			НоваяСтрока.Комментарий = ВыборкаЗапроса.Комментарий;
			НоваяСтрока.Объект = ОбъектСсылка;
			НоваяСтрока.Отпечаток = ВыборкаЗапроса.Отпечаток;
			НоваяСтрока.УстановившийПодпись = ВыборкаЗапроса.УстановившийПодпись;
			НоваяСтрока.ИмяФайлаПодписи = ВыборкаЗапроса.ИмяФайлаПодписи;
			НоваяСтрока.Неверна = Ложь;
			НоваяСтрока.ИндексКартинки = -1;
			НоваяСтрока.Пометка = Истина;
			
			ДвоичныеДанные = ВыборкаЗапроса.Подпись.Получить();
			НоваяСтрока.АдресПодписи = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификаторВызывающейФормы);
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПодписей()
	
	ТаблицаПодписей.Очистить();
	ЗаполнитьСписокПодписейОдногоОбъекта(ОбъектСсылка);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектСсылка = Параметры.Объект;
	ОбъектПроверки = ОбъектСсылка;
	
	Подписан = ОбъектСсылка.Подписан;
	УникальныйИдентификаторВызывающейФормы = Параметры.УникальныйИдентификатор;
	
	Если ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.Файлы") Тогда
		ОбъектСсылка = ОбъектСсылка.ТекущаяВерсия;
	КонецЕсли;	
	
	ЗаполнитьСписокПодписей();
	
КонецПроцедуры


&НаКлиенте
Процедура ОК(Команда)
	
	МассивВозврата = Новый Массив;
	
	Для Каждого Строка Из ТаблицаПодписей Цикл
		Если Строка.Пометка Тогда
			
			СтруктураВозврата = Новый Структура("АдресПодписи, КомуВыданСертификат, ИмяФайлаПодписи", 
				Строка.АдресПодписи, Строка.КомуВыданСертификат, Строка.ИмяФайлаПодписи);
			МассивВозврата.Добавить(СтруктураВозврата);
			
		КонецЕсли;	
	КонецЦикла;	
	
	Закрыть(МассивВозврата);
	
КонецПроцедуры

