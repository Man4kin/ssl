
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуДоступныхРазделов(Параметры.ВидОбработки);
	
	Для Каждого РазделЭлемент Из Параметры.Разделы Цикл
		ИмяРаздела = РазделЭлемент.Значение;
		НайденнаяСтрока = Разделы.НайтиСтроки(Новый Структура("ИмяРаздела", ИмяРаздела));
		Если НайденнаяСтрока.Количество() = 1 Тогда
			НайденнаяСтрока[0].Используется = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДоступныхРазделов(ВидОбработки)
	
	Если ВидОбработки = Перечисления.ВидыДополнительныхОтчетовИОбработок.ДополнительнаяОбработка Тогда
		ТаблицаКоманд = ДополнительныеОтчетыИОбработкиПереопределяемый.ПолучитьОбщиеКомандыДополнительныхОбработок();
	Иначе
		ТаблицаКоманд = ДополнительныеОтчетыИОбработкиПереопределяемый.ПолучитьОбщиеКомандыДополнительныхОтчетов();
	КонецЕсли;
	
	Для Каждого ОписаниеКоманды Из ТаблицаКоманд Цикл
		НоваяСтрока = Разделы.Добавить();
		НоваяСтрока.ИмяРаздела = ОписаниеКоманды.ИмяКоманды;
		НоваяСтрока.Представление = ОписаниеКоманды.ИмяРабочегоМеста;
	КонецЦикла;
	
	Разделы.Сортировать("Представление Возр");
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый СписокЗначений;
	
	Для Каждого РазделЭлемент Из Разделы Цикл
		Если РазделЭлемент.Используется Тогда
			Результат.Добавить(РазделЭлемент.ИмяРаздела);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры
