
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если ЭтаФорма.Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Владелец = Неопределено;
	
	Если Параметры.Отбор.Свойство("_Владелец", Владелец) И ЗначениеЗаполнено(Владелец) Тогда
		Параметры.Отбор.Удалить("_Владелец");
		Параметры.Отбор.Вставить("Владелец", Владелец);
	ИначеЕсли Параметры.Отбор.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		Владелец = Параметры.Отбор.Владелец;
	КонецЕсли;
	
	Если Владелец = Неопределено Тогда
		ЗаголовокСтрока = "";
	Иначе
		ЗаголовокСтрока = СтрПолучитьСтроку(Владелец.СклоненияПредмета, 2);
	КонецЕсли;
	
	Если ПустаяСтрока(ЗаголовокСтрока) Тогда
		Заголовок = НСтр("ru = 'Выберите значение'");
	Иначе
		Заголовок = ЗаголовокСтрока;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПроверитьВозможностьВыбора(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ПроверитьВозможностьВыбора(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВозможностьВыбора(Элемент, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные <> Неопределено  И Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		СтандартнаяОбработка = Ложь;
		Предупреждение(НСтр("ru = 'Выбор групп значений не поддерживается'"));
	КонецЕсли;
	
КонецПроцедуры

