// Заменяет предмет в цепочке взаимодействий.
//
// Параметры
//  Цепочка   - Ссылка - предмет взаимодействий который будет заменен.
//  Предмет	  - Ссылка - предмет, на который будет выполнена замена.
//  Исключать - Ссылка - взаимодействие, в котором операция замены выполнена не будет.
// 
Процедура ЗаменитьПредметВЦепочкеВзаимодействий(Цепочка, Предмет, Исключать) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивВзаимодействий = ПолучитьВзаимодействияИзЦепочки(Цепочка, Исключать);
	
	Для Каждого Взаимодействие Из МассивВзаимодействий Цикл
		Объект = Взаимодействие.ПолучитьОбъект();
		Объект.Заблокировать();
		Объект.Предмет = Предмет;
		Объект.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Получает взаимодействия из цепочки по предмету взаимодействия.
//
// Параметры
//  Цепочка	  - Ссылка - предмет взаимодействия для которого получаются взаимодействия.
//  Исключать - Ссылка - взаимодействие, которое не должно попасть в результирующий массив.
//
// Возвращаемое значение:
//   Массив   - найденные взаимодействия
// 
Функция ПолучитьВзаимодействияИзЦепочки(Цепочка, Исключать) Экспорт
	
	масИмен = Новый Массив;
	масИмен.Добавить("Встреча");
	масИмен.Добавить("ЗапланированноеВзаимодействие");
	масИмен.Добавить("ТелефонныйЗвонок");
	масИмен.Добавить("ЭлектронноеПисьмоВходящее");
	масИмен.Добавить("ЭлектронноеПисьмоИсходящее");
	
	ШаблонТекста = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка
	|ИЗ
	|	Документ.%ИмяТаблицы% КАК Таблица
	|ГДЕ
	|	Таблица.Предмет = &Предмет
	|	И Таблица.Ссылка <> &Исключать";
	СтрОбъединить = "";
	
	ТекстЗапроса = "";
	Для Каждого Имя Из масИмен Цикл
		
		ТекстЗапроса = ТекстЗапроса + СтрОбъединить + СтрЗаменить(ШаблонТекста, "%ИмяТаблицы%", Имя);
		СтрОбъединить = "
		|
		|ОБЪЕДИНИТЬ
		|
		|";
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Предмет",   Цепочка);
	Запрос.УстановитьПараметр("Исключать", Исключать);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции