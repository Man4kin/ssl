////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБЩЕГО НАЗНАЧЕНИЯ

// Выполнить редактирование состава свойств
//
Процедура РедактироватьСоставСвойств(Форма, Ссылка) Экспорт
	
	Наборы = Форма.__Свойства_ОсновнойНабор;
	// Если вернули СправочникСсылка.НаборыДополнительныхРеквизитовИСведений, то это объект с одним набором свойств
	Если ТипЗнч(Наборы) = Тип("СправочникСсылка.НаборыДополнительныхРеквизитовИСведений") Тогда
		Если ЗначениеЗаполнено(Наборы) Тогда
			ПараметрыФормыНабораСвойств = Новый Структура("Ключ", Форма.__Свойства_ОсновнойНабор);
			ОткрытьФорму("Справочник.НаборыДополнительныхРеквизитовИСведений.Форма.ФормаЭлемента", ПараметрыФормыНабораСвойств);
		Иначе
			Предупреждение(НСтр("ru = 'Нельзя получить набор свойств объекта. Возможно не заполнены необходимые реквизиты.'"));
		КонецЕсли;
		
	// ...Иначе - это объект у которого может быть несколько наборов свойств 
	// и требуется выбрать редактируемый набор
	Иначе
		Если Наборы.Количество() = 1 Тогда
			Элемент = Наборы.Получить(0);
		Иначе
			Элемент = Наборы.ВыбратьЭлемент();
			Если Элемент = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		ПараметрыФормыНабораСвойств = Новый Структура("Ключ", Элемент.Значение);
		ОткрытьФорму("Справочник.НаборыДополнительныхРеквизитовИСведений.Форма.ФормаЭлемента", ПараметрыФормыНабораСвойств);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверить, связано ли оповещение с изменением набора свойств и нужно ли 
// обрабатывать его в текущей форме
Функция ОбрабатыватьОповещения(Форма, ИмяСобытия, Параметр) Экспорт
	
	Если (ИмяСобытия <> "ИзмененНаборСвойств") ИЛИ Не Форма.__Свойства_ИспользоватьСвойства Тогда
		Возврат Ложь;
		
	ИначеЕсли Форма.__Свойства_ОсновнойНабор = Параметр Тогда
		Возврат Истина;
		
	ИначеЕсли (ТипЗнч(Форма.__Свойства_ОсновнойНабор) = Тип("СписокЗначений"))
		И (Форма.__Свойства_ОсновнойНабор.НайтиПоЗначению(Параметр) <> Неопределено) Тогда
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции
