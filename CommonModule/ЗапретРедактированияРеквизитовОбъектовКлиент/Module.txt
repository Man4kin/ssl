// Модуль содержит экспортную функцию РазрешитьРедактированиеРеквизитовОбъекта,
// которая умеет разрешать редактирование элементов формы, связанных с заблокированными
// подсистемой реквизитами, а так же формирует запись в реквизите формы
// __ПараметрыЗапретаРедактирования позволяющую запись объекта с изменившимися
// реквизитами.
//
//  __ПараметрыЗапретаРедактирования представляет из себя соответствие, ключем
//  в котором выступает наименование реквизита объекта. Значение - структура
//  с ключами:
//    БлокируемыеЭлементы - Массив - массив, строк - имен блокируемых элементов формы
//    Представление - строка - представление поля на форме (заголовок поля или другое представление)
//    РазрешитьИзменение - булево - изменение реквизита объекта разрешено
//

// Функция реализующая взаимодействие с пользователем 
//
Функция РазрешитьРедактированиеРеквизитовОбъекта(Форма) Экспорт
	
	СинонимыРеквизитов = Новый Массив;
	
	Для Каждого ОписаниеБлокируемогоРеквизита Из Форма.__ПараметрыЗапретаРедактирования Цикл
		СинонимыРеквизитов.Добавить(ОписаниеБлокируемогоРеквизита.Представление);
	КонецЦикла;
	
	МассивСсылок = Новый Массив;
	
	МассивСсылок.Добавить(Форма.Объект.Ссылка);
	
	Результат = ПроверитьСсылкиНаОбъект(МассивСсылок, СинонимыРеквизитов);
	
	Если Результат Тогда
		Для Каждого ОписаниеБлокируемогоРеквизита Из Форма.__ПараметрыЗапретаРедактирования Цикл
			ОписаниеБлокируемогоРеквизита.РазрешитьИзменение = Истина;
		КонецЦикла;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Спрашивает у пользователя подтверждение на разрешение редактирования реквизитов
// и проверяет есть ли ссылки на объект в информационной базе
//
Функция ПроверитьСсылкиНаОбъект(МассивСсылок, СинонимыРеквизитов) Экспорт
	
	Результат = Ложь;
	
	ЗаголовокДиалога = НСтр("ru = 'Редактирование реквизитов объекта'");
	
	РеквизитыПредставление = "";
	
	Для Каждого СинонимРеквизита Из СинонимыРеквизитов Цикл
		РеквизитыПредставление = РеквизитыПредставление + """" + СинонимРеквизита + """, ";
	КонецЦикла;
	
	РеквизитыПредставление = Лев(РеквизитыПредставление, СтрДлина(РеквизитыПредставление)-2);
	
	Если СинонимыРеквизитов.Количество() > 1 Тогда
		ТекстВопроса = НСтр("ru = 'Внимание!
								  |Вы собираетесь редактировать реквизиты %1.
								  |Изменение этих реквизитов может привести к рассогласованию данных информационной базы.'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Внимание!
								  |Вы собираетесь редактировать реквизит %1.
								  |Изменение этого реквизита может привести к рассогласованию данных информационной базы.'");
	КонецЕсли;
	
	Если МассивСсылок.Количество() = 1 Тогда
		ТекстВопроса = ТекстВопроса + НСтр("ru = '
							|Настоятельно рекомендуется предварительно проверить все случаи использования ссылок на данный объект и оценить последствия изменений.
							|Продолжить с проверкой наличия ссылок на этот объект?'");
	Иначе
		ЧислоОбъектов = Строка(МассивСсылок.Количество());
		Склонение = ? (Прав(ЧислоОбъектов,1) = "1", НСтр("ru = 'объекта'"), НСтр("ru = 'объектов'"));
		ТекстВопроса = ТекстВопроса + 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = '
							|Вы уверены, что хотите разрешить редактирование реквизитов для %1 %2?'"), 
								ЧислоОбъектов, Склонение);
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, РеквизитыПредставление);
	
	РезультатВыбора = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ЗаголовокДиалога);
	
	Если РезультатВыбора = КодВозвратаДиалога.Да Тогда
		
		Если МассивСсылок.Количество() = 1 Тогда
			Если ЗапретРедактированияРеквизитовОбъектов.ПроверитьНаОбъектыИмеютсяСсылкиВИБ(МассивСсылок) Тогда
				ТекстСообщения = НСтр("ru = 'Внимание! В информационной базе существуют ссылки на редактируемый объект.
											|Разрешить редактирование реквизитов объекта?'");
				
				РезультатВыбораПослеПоиска = Вопрос(ТекстСообщения,
													РежимДиалогаВопрос.ДаНет, ,
													КодВозвратаДиалога.Да,
													ЗаголовокДиалога);
				Если РезультатВыбораПослеПоиска = КодВозвратаДиалога.Да Тогда
					Результат = Истина;
				КонецЕсли;
				
			Иначе
				ТекстСообщения = НСтр("ru = 'В информационной базе не найдены ссылки на редактируемый объект. Редактирование реквизитов объекта разрешено.'");
				Предупреждение(ТекстСообщения,, ЗаголовокДиалога);
				Результат = Истина;
			КонецЕсли;
			
		Иначе
			Результат = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Устанавливает доступность элементов формы соответствующих реквизитам объекта.
// Если все реквизиты объекта разрешены для редактирования так же
// делает кнопку разрешения редактирования недоступной.
// Параметры
// Форма         - УправляемаяФорма - форма, в которой требуется разрешить
//                 редактирование реквизитов объекта
// Реквизиты - Массив - реквизиты, редактирование которых разрешено
//
Процедура УстановитьДоступностьЭлементовФормы(Форма, знач Реквизиты = Неопределено) Экспорт
	
	Если Реквизиты <> Неопределено Тогда
		Для Каждого Реквизит Из Реквизиты Цикл
			Форма.__ПараметрыЗапретаРедактирования.НайтиСтроки(Новый Структура("ИмяРеквизита", Реквизит))[0].РазрешитьИзменение = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ОписаниеБлокируемогоРеквизита Из Форма.__ПараметрыЗапретаРедактирования Цикл
		Если ОписаниеБлокируемогоРеквизита.РазрешитьИзменение Тогда
			Для Каждого БлокируемыйЭлементФормы Из ОписаниеБлокируемогоРеквизита.БлокируемыеЭлементы Цикл
				ЭлементФормы = Форма.Элементы.Найти(БлокируемыйЭлементФормы.Значение);
				Если ЭлементФормы <> Неопределено Тогда
					Если ТипЗнч(ЭлементФормы) = Тип("ПолеФормы")
					 ИЛИ ТипЗнч(ЭлементФормы) = Тип("ТаблицаФормы") Тогда
						ЭлементФормы.ТолькоПросмотр = Ложь;
					Иначе
						ЭлементФормы.Доступность = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
