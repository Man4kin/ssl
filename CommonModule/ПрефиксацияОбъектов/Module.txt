
// Возвращает признак изменения организации или даты объекта
//
// Параметры:
//  Ссылка – ссылка на объект ИБ.
//  МетаданныеОбъекта – метаданные объекта из параметра Ссылка.
//  ДатаПослеИзменения – дата объекта после изменения
//  ОрганизацияПослеИзменения – организация объекта после изменения.
// 
//  Возвращаемое значение:
//   Тип: Булево. Истина – организация объекта была изменена или новая дата объекта 
// задана в другом интервале периодичности по сравнению с предыдущим значением даты.
// Ложь – организация и дата документа не были изменены.
//
Функция ДатаИлиОрганизацияОбъектаИзменена(Ссылка, МетаданныеОбъекта, Знач ДатаПослеИзменения, Знач ОрганизацияПослеИзменения) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ШапкаОбъекта.Дата                                КАК Дата,
	|	ЕСТЬNULL(ШапкаОбъекта.Организация.Префикс, """") КАК ПрефиксОрганизацииДоИзменения
	|ИЗ
	|	" + МетаданныеОбъекта.ПолноеИмя() + " КАК ШапкаОбъекта
	|ГДЕ
	|	ШапкаОбъекта.Ссылка = &Ссылка
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ПрефиксОрганизацииПослеИзменения = ПолучитьФункциональнуюОпцию("ПрефиксыОрганизаций", Новый Структура("Организация", ОрганизацияПослеИзменения));
	
	// если задана пустая ссылка на организацию
	ПрефиксОрганизацииПослеИзменения = ?(ПрефиксОрганизацииПослеИзменения = Ложь, "", ПрефиксОрганизацииПослеИзменения);
	
	Возврат Выборка.ПрефиксОрганизацииДоИзменения <> ПрефиксОрганизацииПослеИзменения
		ИЛИ Не ДатыОбъектаОдногоПериода(Выборка.Дата, ДатаПослеИзменения, МетаданныеОбъекта);
	//
КонецФункции

// Возвращает признак изменения организации объекта
//
// Параметры:
//  Ссылка – ссылка на объект ИБ.
//  МетаданныеОбъекта – метаданные объекта из параметра Ссылка.
//  ОрганизацияПослеИзменения – организация объекта после изменения.
//
//  Возвращаемое значение:
//  Тип: Булево. Истина – организация объекта была изменена. Ложь – организация не была изменена.
//
Функция ОрганизацияОбъектаИзменена(Ссылка, МетаданныеОбъекта, Знач ОрганизацияПослеИзменения) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ШапкаОбъекта.Организация.Префикс, """") КАК ПрефиксОрганизацииДоИзменения
	|ИЗ
	|	" + МетаданныеОбъекта.ПолноеИмя() + " КАК ШапкаОбъекта
	|ГДЕ
	|	ШапкаОбъекта.Ссылка = &Ссылка
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ПрефиксОрганизацииПослеИзменения = ПолучитьФункциональнуюОпцию("ПрефиксыОрганизаций", Новый Структура("Организация", ОрганизацияПослеИзменения));
	
	// если задана пустая ссылка на организацию
	ПрефиксОрганизацииПослеИзменения = ?(ПрефиксОрганизацииПослеИзменения = Ложь, "", ПрефиксОрганизацииПослеИзменения);
	
	Возврат Выборка.ПрефиксОрганизацииДоИзменения <> ПрефиксОрганизацииПослеИзменения;
		
	//
КонецФункции

// Определяет признак равенства двух дат для объекта метаданных.
// Даты считаются равными, если они принадлежат одному периоду времени: Год, Месяц, День и пр.
//
// Параметры:
// Дата1 – первая дата для сравнения;
// Дата2 – вторая дата для сравнения;
// МетаданныеОбъекта – метаданные объекта, для которого необходимо получить значение функции.
// 
//  Возвращаемое значение:
//  Тип: Булево. Истина – даты объекта одного периода; Ложь – даты объекта разных периодов.
//
Функция ДатыОбъектаОдногоПериода(Знач Дата1, Знач Дата2, МетаданныеОбъекта) Экспорт
	
	Если ПериодичностьНомераДокументаГод(МетаданныеОбъекта) Тогда
		
		РазностьДат = НачалоГода(Дата1) - НачалоГода(Дата2);
		
	ИначеЕсли ПериодичностьНомераДокументаКвартал(МетаданныеОбъекта) Тогда
		
		РазностьДат = НачалоКвартала(Дата1) - НачалоКвартала(Дата2);
		
	ИначеЕсли ПериодичностьНомераДокументаМесяц(МетаданныеОбъекта) Тогда
		
		РазностьДат = НачалоМесяца(Дата1) - НачалоМесяца(Дата2);
		
	ИначеЕсли ПериодичностьНомераДокументаДень(МетаданныеОбъекта) Тогда
		
		РазностьДат = НачалоДня(Дата1) - НачалоДня(Дата2);
		
	Иначе // ПериодичностьНомераДокументаНеопределено
		
		РазностьДат = 0;
		
	КонецЕсли;
	
	Возврат РазностьДат = 0;
	
КонецФункции

//

Функция ПериодичностьНомераДокументаГод(МетаданныеОбъекта)
	
	Возврат МетаданныеОбъекта.ПериодичностьНомера = Метаданные.СвойстваОбъектов.ПериодичностьНомераДокумента.Год;
	
КонецФункции

Функция ПериодичностьНомераДокументаКвартал(МетаданныеОбъекта)
	
	Возврат МетаданныеОбъекта.ПериодичностьНомера = Метаданные.СвойстваОбъектов.ПериодичностьНомераДокумента.Квартал;
	
КонецФункции

Функция ПериодичностьНомераДокументаМесяц(МетаданныеОбъекта)
	
	Возврат МетаданныеОбъекта.ПериодичностьНомера = Метаданные.СвойстваОбъектов.ПериодичностьНомераДокумента.Месяц;
	
КонецФункции

Функция ПериодичностьНомераДокументаДень(МетаданныеОбъекта)
	
	Возврат МетаданныеОбъекта.ПериодичностьНомера = Метаданные.СвойстваОбъектов.ПериодичностьНомераДокумента.День;
	
КонецФункции
