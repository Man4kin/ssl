////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СИСТЕМНЫХ СОБЫТИЙ

// Процедура УведомлятьОНекорректномСостоянииОбработкиРегламентныхЗаданий уведомляет
// пользователей о невыполнении регламентных заданий и "зависании" фоновых заданий.
//  Процедура работает только если информационная база файловая. Для серверной ИБ
// ответственность за выполнение регламентных заданий лежит на администраторе сервера 1С.
//  Подключает в процедуре РегламентныеЗаданияКлиент.ПриНачалеРаботыСистемы().
//
Процедура УведомлятьОНекорректномСостоянииОбработкиРегламентныхЗаданий() Экспорт

	ПериодУведомления = Неопределено; // Минут.
	Если РегламентныеЗаданияСервер.НужноУведомлятьОНекорректномСостоянииОбработкиРегламентныхЗаданий(ПериодУведомления) Тогда
		ОписаниеОшибки = "";
		ЗаданияОбрабатываютсяНормально = Неопределено;
		РегламентныеЗаданияСервер.ТекущийСеансОбрабатываетЗадания(ЗаданияОбрабатываютсяНормально, , ОписаниеОшибки);
		Если НЕ ЗаданияОбрабатываютсяНормально Тогда
			РегламентныеЗаданияКлиент.ПриОшибкеОбработкиРегламентныхЗаданий(ОписаниеОшибки);
		КонецЕсли;
		ПодключитьОбработчикОжидания("УведомлятьОНекорректномСостоянииОбработкиРегламентныхЗаданий", ПериодУведомления * 60, Истина);
	КонецЕсли;

КонецПроцедуры // УведомлятьОНекорректномСостоянииОбработкиРегламентныхЗаданий()

// Процедура ОбработкаРегламентныхЗаданийВОсновномСеансе() вызывается
// обработчиком ожидания, который подключается в
// РегламентныеЗаданияКлиент.ПриНачалеРаботыСистемы().
//  Для обработки в отдельном сеансе применяется форма
// Обработки.РегламентныеИФоновыеЗадания.Форма.ОбработкаРегламентныхЗаданий.
//
Процедура ОбработкаРегламентныхЗаданийВОсновномСеансе() Экспорт

	Если РегламентныеЗаданияСервер.ТекущийСеансОбрабатываетЗадания() Тогда
		РегламентныеЗаданияСервер.ОбработатьРегламентныеЗадания(, Истина);
		ПодключитьОбработчикОжидания("ОбработкаРегламентныхЗаданийВОсновномСеансе", 60, Истина);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаРегламентныхЗаданийВОсновномСеансе()

Процедура ОткрытьОтдельныйСеансОбработкиРегламентныхЗаданийЧерезОбработчикОжидания() Экспорт
	
	Результат = РегламентныеЗаданияКлиент.ОткрытьОтдельныйСеансОбработкиРегламентныхЗаданий();
	
	Если Результат.Отказ  Тогда
		Предупреждение(Результат.ОписаниеОшибки);
		
	ИначеЕсли Результат.ВыполненаПопыткаОткрытия Тогда
		
		ПодключитьОбработчикОжидания("АктивизироватьГлавноеОкноТекущегоСеансаПослеЗапускаОтдельногоСеансаОбработкиРегламентныхЗаданий", 2, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура АктивизироватьГлавноеОкноТекущегоСеансаПослеЗапускаОтдельногоСеансаОбработкиРегламентныхЗаданий() Экспорт
	
	ОсновноеОкно = РегламентныеЗаданияКлиент.ОсновноеОкно();
	Если ОсновноеОкно <> Неопределено Тогда
		ОсновноеОкно.Активизировать();
	КонецЕсли;
	
КонецПроцедуры

