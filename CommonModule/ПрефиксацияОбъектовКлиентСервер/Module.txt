////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ ФОРМИРОВАНИЯ НОМЕРА/КОДА ОБЪЕКТА ДЛЯ ВЫВОДА В ПЕЧАТНЫХ ФОРМАХ

// Удаляет префикс информационной базы и префикс организации из переданной строки НомерОбъекта
// Переменная НомерОбъекта должна соответствовать шаблону: ООГГ-ХХХ...ХХ, где
// ОО - префикс организации;
// ГГ - префикс информационной базы;
// "-" - разделитель;
// ХХХ...ХХ - номер/код объекта.
//
// Параметры:
//  НомерОбъекта - Строка - номер или код объекта из которого требуется удалить префиксы
//  УдалитьПрефиксОрганизации - Булево (необязательный) - признак удаления префикса организации; по умолчанию равен Ложь;
//  УдалитьПрефиксИнформационнойБазы - Булево (необязательный) - признак удаления префикса информационной базы; по умолчанию равен Ложь;
//
// Примеры:
//  УдалитьПрефиксыИзНомераОбъекта("НФГЛ-000001234", Истина, Истина) = "000001234"
//  УдалитьПрефиксыИзНомераОбъекта("НФГЛ-000001234", Ложь, Истина)   = "НФ-000001234"
//  УдалитьПрефиксыИзНомераОбъекта("НФГЛ-000001234", Истина, Ложь)   = "ГЛ-000001234"
//  УдалитьПрефиксыИзНомераОбъекта("НФГЛ-000001234", Ложь, Ложь)     = "НФГЛ-000001234"
//
Функция УдалитьПрефиксыИзНомераОбъекта(Знач НомерОбъекта, УдалитьПрефиксОрганизации = Ложь, УдалитьПрефиксИнформационнойБазы = Ложь) Экспорт
	
	// изначально пустая строка префикса номера объекта
	ПрефиксОбъекта = "";
	
	ПрефиксОрганизации        = Лев(НомерОбъекта, 2);
	ПрефиксИнформационнойБазы = Сред(НомерОбъекта, 3, 2);
	
	// добавляем префикс организации
	Если Не УдалитьПрефиксОрганизации Тогда
		
		ПрефиксОбъекта = ПрефиксОбъекта + ПрефиксОрганизации;
		
	КонецЕсли;
	
	// добавляем префикс информационной базы
	Если Не УдалитьПрефиксИнформационнойБазы Тогда
		
		ПрефиксОбъекта = ПрефиксОбъекта + ПрефиксИнформационнойБазы;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПрефиксОбъекта) Тогда
		
		ПрефиксОбъекта = ПрефиксОбъекта + "-";
		
	КонецЕсли;
	
	Возврат ПрефиксОбъекта + Сред(НомерОбъекта, 6);
КонецФункции

// Удаляет лидирующие нули из номера объекта
// Переменная НомерОбъекта должна соответствовать шаблону: ООГГ-ХХХ...ХХ, где
// ОО - префикс организации;
// ГГ - префикс информационной базы;
// "-" - разделитель;
// ХХХ...ХХ - номер/код объекта.
//
// Параметры:
//  НомерОбъекта - Строка - номер или код объекта из которого требуется лидирующие нули
// 
Функция УдалитьЛидирующиеНулиИзНомераОбъекта(Знач НомерОбъекта) Экспорт
	
	Префикс = Лев(НомерОбъекта, 5);
	ПользовательскийПрефикс = ПолучитьПользовательскийПрефикс(НомерОбъекта);
	
	Номер = Сред(НомерОбъекта, 6 + СтрДлина(ПользовательскийПрефикс));
	
	// удаляем лидирующие нули слева в номере
	Номер = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(Номер, "0");
	
	Возврат Префикс + ПользовательскийПрефикс + Номер;
КонецФункции

// Удаляет все пользовательские префиксы из номера объекта (все нецифровые символы)
// Переменная НомерОбъекта должна соответствовать шаблону: ООГГ-ХХХ...ХХ, где
// ОО - префикс организации;
// ГГ - префикс информационной базы;
// "-" - разделитель;
// ХХХ...ХХ - номер/код объекта.
//
// Параметры:
//  НомерОбъекта - Строка - номер или код объекта из которого требуется лидирующие нули
// 
Функция УдалитьПользовательскиеПрефиксыИзНомераОбъекта(Знач НомерОбъекта) Экспорт
	
	СтрокаЦифровыхСимволов = "0123456789";
	
	Префикс     = Лев(НомерОбъекта, 5);
	НомерПолный = Сред(НомерОбъекта, 6);
	
	Номер = "";
	
	Для Индекс = 1 По СтрДлина(НомерПолный) Цикл
		
		Символ = Сред(НомерПолный, Индекс, 1);
		
		Если Найти(СтрокаЦифровыхСимволов, Символ) > 0 Тогда
			
			Номер = Номер + Символ;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Префикс + Номер;
КонецФункции

// Получает пользовательский префикс номера/кода объекта
// Переменная НомерОбъекта должна соответствовать шаблону: ООГГ-ААХХ..ХХ, где
// ОО - префикс организации;
// ГГ - префикс информационной базы;
// "-" - разделитель;
// АА - пользовательский префикс;
// ХХ..ХХ - номер/код объекта.
//
// Параметры:
//  НомерОбъекта - Строка - номер или код объекта из которого требуется получить пользовательский префикс
// 
Функция ПолучитьПользовательскийПрефикс(Знач НомерОбъекта) Экспорт
	
	// возвращаемое значение функции (пользовательский префикс)
	Результат = "";
	
	НомерОбъекта = Сред(НомерОбъекта, 6);
	
	СтрокаЦифровыхСимволов = "0123456789";
	
	Для Индекс = 1 По СтрДлина(НомерОбъекта) Цикл
		
		Символ = Сред(НомерОбъекта, Индекс, 1);
		
		Если Найти(СтрокаЦифровыхСимволов, Символ) > 0 Тогда
			Прервать;
		КонецЕсли;
		
		Результат = Результат + Символ;
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Получает номер документа для вывода на печать; из номера удаляются префиксы и лидирующие нули
// Функция:
// отбрасывает префикс организации,
// отбрасывает префикс информационной базы (опционально),
// отбрасывает пользовательские префиксы (опционально),
// удаляет лидирующие нули в номере объекта
//
Функция ПолучитьНомерНаПечать(Знач НомерОбъекта, УдалитьПрефиксИнформационнойБазы = Ложь, УдалитьПользовательскийПрефикс = Ложь) Экспорт
	
	// удаляем пользовательские префиксы из номера объекта
	Если УдалитьПользовательскийПрефикс Тогда
		
		НомерОбъекта = УдалитьПользовательскиеПрефиксыИзНомераОбъекта(НомерОбъекта);
		
	КонецЕсли;
	
	// удаляем лидирующие нули из номера объекта
	НомерОбъекта = УдалитьЛидирующиеНулиИзНомераОбъекта(НомерОбъекта);
	
	// удаляем префикс организации и префикс информационной базы из номера объекта
	НомерОбъекта = УдалитьПрефиксыИзНомераОбъекта(НомерОбъекта, Истина, УдалитьПрефиксИнформационнойБазы);
	
	Возврат НомерОбъекта;
КонецФункции
