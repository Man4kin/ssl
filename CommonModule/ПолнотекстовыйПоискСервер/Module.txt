////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ УПРАВЛЕНИЯ ИНДЕКСОМ ПОЛНОТЕКСТОВОГО ПОИСКА

// Обновляет индекс полнотекстового поиска
Процедура ОбновлениеИндексаППД() Экспорт
	
	РазрешитьПолнотекстовыйПоиск = ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить;
	Если РазрешитьПолнотекстовыйПоиск = Ложь Тогда
		Возврат;
	КонецЕсли;	
	
	Попытка
		ЗаписьЖурналаРегистрации("Полнотекстовое индексирование",
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Начато регламентное индексирование порции'"));
		
		ПолнотекстовыйПоиск.ОбновитьИндекс(Ложь, Истина);
		
		ЗаписьЖурналаРегистрации("Полнотекстовое индексирование",
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Закончено регламентное  индексирование порции'"));
	Исключение
		ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		  НСтр("ru = 'Во время регламентного обновления индекса произошла неизвестная ошибка.
		             |%1'"), ОписаниеОшибки());
		ЗаписьЖурналаРегистрации("Полнотекстовое индексирование",
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстСообщенияОбОшибке);
	КонецПопытки;
	
КонецПроцедуры

// Выполняет слияние индексов полнотекстового поиска
Процедура СлияниеИндексаППД() Экспорт
	
	РазрешитьПолнотекстовыйПоиск = ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить;
	Если РазрешитьПолнотекстовыйПоиск = Ложь Тогда
		Возврат;
	КонецЕсли;	
	
	Попытка
		ЗаписьЖурналаРегистрации("Полнотекстовое индексирование",
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Начато регламентное слияние индексов'"));
		
		ПолнотекстовыйПоиск.ОбновитьИндекс(Истина);
		
		ЗаписьЖурналаРегистрации("Полнотекстовое индексирование",
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Закончено регламентное слияние индексов'"));
	Исключение
		ТекстСообщенияОбОшибке =
		  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		    НСтр("ru = 'Во время регламентного слияния индекса произошла неизвестная ошибка.
		               |%1'"), ОписаниеОшибки());
		ЗаписьЖурналаРегистрации("Полнотекстовое индексирование",
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстСообщенияОбОшибке);
	КонецПопытки;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы.

// Установить константу ИспользоватьПолнотекстовыйПоиск синхронно с ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска()
Процедура ИнициализироватьФункциональнуюОпциюПолнотекстовыйПоиск() Экспорт
	Если ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить Тогда
		Константы.ИспользоватьПолнотекстовыйПоиск.Установить(Истина);
	Иначе
		Константы.ИспользоватьПолнотекстовыйПоиск.Установить(Ложь);
	КонецЕсли;	
КонецПроцедуры

