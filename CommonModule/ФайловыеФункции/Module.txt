// Извлекает текст из файлов на диске
Процедура ИзвлечениеТекста() Экспорт
	
	ТипПлатформыСервера = РаботаСФайламиПовтИсп.ТипПлатформыСервера();
	Если ТипПлатформыСервера <> ТипПлатформы.Windows_x86 И ТипПлатформыСервера <> ТипПлатформы.Windows_x86_64 Тогда
		Возврат;  // извлечение текста работает только под Windows
	КонецЕсли;	
	
	ИмяСРасширениемФайла = "";	
	
	Попытка	
		ЗаписьЖурналаРегистрации("Извлечение текста", 
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Начато регламентное извлечение текста'"));
			
		Запрос = Новый Запрос;
		
		Запрос.Текст = 			
		 "ВЫБРАТЬ ПЕРВЫЕ 100
		 |	ВерсииФайлов.Ссылка КАК Ссылка,
		 |	ВерсииФайлов.СтатусИзвлеченияТекста КАК СтатусИзвлеченияТекста,
		 |	ВерсииФайлов.ТипХраненияФайла КАК ТипХраненияФайла
		 |ИЗ
		 |	Справочник.ВерсииФайлов КАК ВерсииФайлов
		 |ГДЕ
		 |	(ВерсииФайлов.СтатусИзвлеченияТекста = &Статус
		 |			ИЛИ ВерсииФайлов.СтатусИзвлеченияТекста = ЗНАЧЕНИЕ(Перечисление.СтатусыИзвлеченияТекстаФайлов.ПустаяСсылка))
		 |	И ВерсииФайлов.Зашифрован = &Зашифрован";
		
		Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыИзвлеченияТекстаФайлов.НеИзвлечен);
		Запрос.УстановитьПараметр("Зашифрован", Ложь);
		
		Результат = Запрос.Выполнить();
		ТаблицаВыгрузки = Результат.Выгрузить();
		
		Для Каждого Строка Из ТаблицаВыгрузки Цикл
			ТекущаяВерсия = Строка.Ссылка.ПолучитьОбъект();
			
			ИмяСРасширениемФайла = ТекущаяВерсия.ПолноеНаименование  + "." + ТекущаяВерсия.Расширение;
			
			ИмяФайлаСПутем = "";
			ТипХраненияФайла = Строка.ТипХраненияФайла;
			
			Попытка
			
				Если ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе Тогда
					ДвоичныеДанныеФайла = ТекущаяВерсия.ФайлХранилище.Получить();
					
					ИмяФайлаСПутем = ПолучитьИмяВременногоФайла(ТекущаяВерсия.Расширение);
					// Сохраним файл из БД на диск
					ДвоичныеДанныеФайла.Записать(ИмяФайлаСПутем);
				Иначе // тут файл на диске
					
					Если НЕ ТекущаяВерсия.Том.Пустая() Тогда
						ИмяФайлаСПутем = РаботаСФайлами.ПолныйПутьТома(ТекущаяВерсия.Том) + ТекущаяВерсия.ПутьКФайлу; 
					КонецЕсли;
					
				КонецЕсли;
				
				
				Текст = "";
				
				Если ИмяФайлаСПутем <> "" Тогда
					// Извлекаем текст из файла
					Извлечение = Новый ИзвлечениеТекста(ИмяФайлаСПутем);
					Текст = Извлечение.ПолучитьТекст();
					ТекущаяВерсия.СтатусИзвлеченияТекста = Перечисления.СтатусыИзвлеченияТекстаФайлов.Извлечен;
				КонецЕсли;
			
			Исключение // Ничего не пишем - это нормальная ситуация - когда Текст некому извлечь
				ТекущаяВерсия.СтатусИзвлеченияТекста = Перечисления.СтатусыИзвлеченияТекстаФайлов.ИзвлечьНеУдалось;
			КонецПопытки;
			
			Если ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе Тогда
				УдалитьФайлы(ИмяФайлаСПутем);
			КонецЕсли;

			ТекущаяВерсия.ТекстХранилище = Новый ХранилищеЗначения(Текст, Новый СжатиеДанных);

			ФайлЗаблокирован = Ложь;
			
			Файл = ТекущаяВерсия.Владелец;
			Если Файл.ТекущаяВерсия = Строка.Ссылка Тогда
				Попытка
					ЗаблокироватьДанныеДляРедактирования(Файл);
					ФайлЗаблокирован = Истина;
				Исключение
					Продолжить; // ничего не сообщаем, обрабатываем следующий объект ВерсииФайлов
				КонецПопытки;
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Истина);
			НачатьТранзакцию();
			Попытка
				ТекущаяВерсия.Записать();

				Если Файл.ТекущаяВерсия = Строка.Ссылка Тогда
					ФайлОбъект = Файл.ПолучитьОбъект();
					ФайлОбъект.ТекстХранилище = ТекущаяВерсия.ТекстХранилище;
					ФайлОбъект.Записать();
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				УстановитьПривилегированныйРежим(Ложь);
				
				Если ФайлЗаблокирован Тогда
					РазблокироватьДанныеДляРедактирования(Файл);
				КонецЕсли;
				
			Исключение
				ОтменитьТранзакцию();
				УстановитьПривилегированныйРежим(Ложь);
				
				Если ФайлЗаблокирован Тогда
					РазблокироватьДанныеДляРедактирования(Файл);
				КонецЕсли;
				
				ВызватьИсключение;
			КонецПопытки;
			
		КонецЦикла;
		
		ЗаписьЖурналаРегистрации("Извлечение текста", 
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Закончено регламентное извлечение текста'"));
	Исключение

		ОписаниеОшибкиИнфо = ОписаниеОшибки();
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								 НСтр("ru = 'Во время регламентного извлечения текста из файла ""%1"" произошла неизвестная ошибка.'"), 
								 ИмяСРасширениемФайла);
		ТекстСообщения = ТекстСообщения + Строка(ОписаниеОшибкиИнфо);
		
		ЗаписьЖурналаРегистрации("Извлечение текста", 
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстСообщения);
			
	КонецПопытки;
КонецПроцедуры

